/* %- Rcs-Info 

  $Header: /home/florenz/opal/home_uebb_CVS/CVS/ocs/examples/Reflections/Guesses/Game.impl,v 1.1 1999-03-23 12:28:23 kd Exp $
  $Author: kd $
  $Date: 1999-03-23 12:28:23 $
  $State: Exp $

  $Locker:  $
  $Revision: 1.1 $
  $Name: not supported by cvs2svn $

  $Log: not supported by cvs2svn $
  Revision 1.4  1998/12/10 23:52:51  silver
  Pragma used now.

  Revision 1.3  1998/11/03 14:18:37  silver
  *** empty log message ***

  Revision 1.2  1998/09/28 15:02:46  silver
  Made file compilable.

  Revision 1.1  1998/06/13 20:13:23  silver
  Initial revision

*/

IMPLEMENTATION Game

IMPORT	ComCompose		COMPLETELY
	Com			COMPLETELY
	Random			COMPLETELY
	Nat			COMPLETELY

	OStream			COMPLETELY
	OStreamShift		COMPLETELY
	IStream			COMPLETELY
	IStreamShift		COMPLETELY


-- %$The actual game$

FUN guessLoop : nat ** nat -> com[nat]
DEF guessLoop (i, secret) ==
  
  -- Well, this is the i-th guess:
  cout << "This is your " << i << ". guess. "; 

  -- Read a number between 1 and 100:
  cin ("Please enter a number between 1 and 100: ")
    | (_ >= 1) | (_ <= 100) >> (\\ num .

  IF num < secret THEN
    -- Nope, too small
    cout << "Sorry, but " << num << " is too small." << endl;
    guessLoop (i+1, secret)
  IF num > secret THEN
    -- Nope, too big
    cout << "Sorry, but " << num << " is too large." << endl;
    guessLoop (i+1, secret)
  IF num = secret THEN
    -- Well, correct...
    succeed (i)
  FI)
  

-- %$The main game$

DEF Game ==

  -- Introduction
  cout << "Hello, this is a game. I have guessed a number" << endl;
  cout << "between 1 one 100. You must try to guess the" << endl;
  cout << "number. I will tell you whether your bet was too" << endl;
  cout << "high or too low. Let's begin!" << endl << endl; (

  -- Generate a random number
  random(100) & (\\ secret .

  -- The actual game! It returns the number of guesses needed.
  guessLoop (1, secret+1) & (\\ i.

  -- Done
  cout << "You guessed my number after " << i << " tries!" << endl; (

  -- Print some encouraging text
  IF i < 1 THEN cout << "You hacked the program!!!" << endl
  IF i = 1 THEN cout << "You knew the seed value!" << endl
  IF i = 2 THEN cout << "You must be psychic!" << endl
  IF i = 3 THEN cout << "That was fantastic!" << endl
  IF i = 4 THEN cout << "That was excellent!" << endl
  IF i = 5 THEN cout << "That was very good." << endl
  IF i = 6 THEN cout << "That was ok." << endl
  IF i = 7 THEN cout << "That was not so good." << endl
  IF i > 7 THEN cout << "Perhaps you should try to think "
                     << "before you type?" << endl
  FI))))


