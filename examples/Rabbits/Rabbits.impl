IMPLEMENTATION Rabbits

IMPORT  Denotation      ONLY  ++
        Nat             ONLY nat ! 0 1 2 3 10 - + > = %
        NatConv         ONLY `
        Com             COMPLETELY
        ComCompose      COMPLETELY
        BasicIO         COMPLETELY

-- FUN main : com[void] -- already declared in signature part
DEF main ==
  ask("For which generation do you " ++ 
      "want to know the number of rabbits? ") & (\\ generation. 
  writeLine(result(generation, rabbits(generation)))
  )

-- print result in nice form
FUN result: nat ** nat -> denotation
DEF result(generation, bunnys) ==
  "In the " 
  ++ (generation`)
  ++ ordinal(generation)
  ++ " generation there are " 
  ++ (bunnys`) 
  ++ " couples of rabbits." 

FUN ordinal: nat -> denotation
DEF ordinal(n) ==
  IF n % 10 = 1 THEN "st"
  IF n % 10 = 2 THEN "nd"
  IF n % 10 = 3 THEN "rd"
  ELSE               "th"
  FI

-- --------------------------------------------------------------

FUN rabbits : nat -> nat
DEF rabbits(generation) ==
        IF generation = 0 THEN 1
        IF generation = 1 THEN 1
        IF generation > 1 THEN rabbits(generation - 1) 
                               + rabbits(generation - 2)
        FI