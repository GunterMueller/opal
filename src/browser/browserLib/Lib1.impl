IMPLEMENTATION Lib1[data]
-- ============================================================================
-- Autor:	ralfi
-- Projekt:	Browser
-- Zweck:       Libraryfunktionen
-- Version: $Id$
-- ============================================================================

IMPORT	Lib		ONLY	nl
	Seq		ONLY	% :: <> seq
	String		ONLY	! : denotation -> string ++ string

FUN seq2StringByNewLine	: (data -> string) -> seq[data] -> string
DEF seq2StringByNewLine(F)(S) == seq2StringByList(""!,nl,nl,F)(S)

FUN seq2StringBySpace	: (data -> string) -> seq[data] -> string
DEF seq2StringBySpace (F)(S) == seq2StringByList(""!," "!,""!,F)(S)

FUN seq2StringByList	: string ** string ** string ** (data -> string) -> 
				seq[data] -> string
DEF seq2StringByList(St,_,En,F)(<>) == St ++ En
DEF seq2StringByList(St,Tr,En,F)(El:: <>) == (St ++ (F(El))) ++ En
DEF seq2StringByList(St,Tr,En,F)(El::RestSeq) ==
		(St ++ (F(El))) ++ (s2SBL2(Tr,F)(RestSeq) ++ En)

FUN s2SBL2	: string ** (data -> string) -> seq[data] -> string
DEF s2SBL2(_,F)(<>) == ""!
DEF s2SBL2(Tr,F)(Ft::Rt) == Tr ++ ((Ft F) ++ (s2SBL2(Tr,F)(Rt)))

FUN uniq	: (data**data->bool)->seq[data]->seq[data]
DEF uniq(_)(<>) == <>
DEF uniq(_)(Ft:: <>) == Ft%
DEF uniq(Eq)(Ft::(Se::Rt)) == 
 	IF Ft Eq Se THEN uniq(Eq)(Se::Rt)
 	ELSE Ft::uniq(Eq)(Se::Rt)
 	FI
	
