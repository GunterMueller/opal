
IMPLEMENTATION SourceAnFun
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date$ ($Revision$)

IMPORT	Denotation ONLY ++
	Nat	COMPLETELY
	NatConv ONLY `
	Seq	COMPLETELY
	Option	ONLY option

IMPORT	Format	COMPLETELY
	FormatFun1 COMPLETELY

IMPORT	Ide	ONLY format:ide->format
	SourceAn COMPLETELY


-- ---------------------------------------------------------------------------
DEF symbol(a,i) ==
    IF symbol?(a) THEN symbol(a)
    ELSE pretty(format(i),max:nat) FI

-- ---------------------------------------------------------------------------

DEF format(a:sourceAn) ==
    IF pos?(a) THEN
	blk(2,%(str("{"),str(`(pos(a))),str("}")))
    IF error?(a) THEN
	blk(2,%(str("{"),str(`(pos(a))),str(","),
		str(message(a)),str(","),
		str(IF never?(kind(a))    THEN "never"
		    IF optional?(kind(a)) THEN "optional"
		    IF always?(kind(a))   THEN "always" FI),
		str("}")))
    IF symbol?(a) THEN
	blk(2,%(str("{"),str(symbol(a)),str("}")))
    IF version?(a) THEN
	blk(2,%(str("version="""),str(version(a)),str("""")))
    ELSE
	str("")
    FI


FUN ` : pos -> denotation
DEF `(p) == `(line(p)) ++ "." ++ `(colum(p))

