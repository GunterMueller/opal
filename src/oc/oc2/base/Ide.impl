IMPLEMENTATION Ide
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/doc/LICENSE or
      http://projects.uebb.tu-berlin.de/opal/trac/wiki/License for details
*/
-- % $Date$ ($Revision$)

IMPORT	Nat	COMPLETELY
	String	ONLY string
	Seq	ONLY seq %
	Option	ONLY option
	NatConv ONLY `

IMPORT	Format	COMPLETELY
 

DATA ide ==
	global	(# : nat, origin: origin)
	local	(# : nat)


DEF (i1:ide)=i2 ==
    IF local?(i1) THEN
	IF local?(i2) THEN #(i1) = #(i2) ELSE false FI
    OTHERWISE
    IF local?(i2) THEN
	false
    OTHERWISE
    IF #(origin(i1)) = #(origin(i2)) THEN #(i1) = #(i2) 
    ELSE false FI

DEF (i1:ide)<i2 ==
    IF local?(i1) THEN
	IF local?(i2) THEN #(i1) < #(i2) ELSE true FI
    OTHERWISE
    IF local?(i2) THEN
	false
    OTHERWISE
    IF #(origin(i1)) < #(origin(i2)) THEN true
    IF #(origin(i1)) = #(origin(i2)) THEN #(i1) < #(i2)
    ELSE false FI
    

DEF format(global(#,o)) ==
    IF o = target THEN
        blk(0,%(str("$"),str(`(#))))
    ELSE
        blk(0,%(str("$"),str(`(#)),format(o)))
    FI

DEF format(local(#)) ==
    blk(0,%(str(`(#))))

-- =========================================================================

DATA origin == origin(# : nat)

DEF target == origin(0)

DEF origin(n1) = origin(n2) == n1 = n2
DEF origin(n1) < origin(n2) == n1 < n2


DEF format(origin(#)) ==
    blk(0,%(str("'"),str(`(#))))

