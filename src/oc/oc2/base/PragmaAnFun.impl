IMPLEMENTATION PragmaAnFun
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date: 1999-10-06 21:26:51 $ ($Revision: 1.2 $)

-- subject: miscelleanous functions related to pragma annotations
-- author:  wg 

IMPORT	Ide		COMPLETELY
	PragmaAn	COMPLETELY
	TypeAn		ONLY type:SORT
	TypeAnFun	ONLY format:type->format
	Format		COMPLETELY
	FormatFun1	COMPLETELY
	
IMPORT	Nat		ONLY nat 0 1
	NatConv		ONLY `
	Seq		COMPLETELY



-- ---------------------------------------------------------------------------

DEF format(a) ==
    IF pragmas?(a) THEN
	list(str(""),nl,str(""),format)(pragmas(a))
    ELSE
	str("no pragmas")
    FI

DEF format(pragma(Items)) ==
    list(str("/$ "),str(" "),str(" $/"),format)(Items)

FUN format : pragmaItem -> format
DEF format(lexem(_,Val)) == str(Val)
DEF format(names(_,Nms)) == list(str("["),str(","),str("]"),format)(Nms)

FUN format : pragmaName -> format
DEF format(type(_,Type)) == 
    format(Type)
DEF format(val(_,Type,Id,Inst)) == 
    blk(0,%(format(Id),str(":"),brk(1),format(Type),
	    IF Inst <>? THEN str("")
			ELSE list(str("["),str(","),str("]"),format)(Inst)
			FI
	    ))
DEF format(param(_,Type,No)) ==
    blk(0,%(str("&"),str(No`),str(":"),brk(1),format(Type)))

-- ---------------------------------------------------------------------------

