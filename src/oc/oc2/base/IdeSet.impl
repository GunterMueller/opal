
IMPLEMENTATION IdeSet 
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/doc/LICENSE or
      http://projects.uebb.tu-berlin.de/opal/trac/wiki/License for details
*/
-- % $Date$ ($Revision$)

IMPORT  
	Seq	COMPLETELY

IMPORT	Ide	ONLY ide < : ide**ide->bool 
	SetByBST COMPLETELY
	SetByBSTFilter  ONLY |
        SetByBSTConv    ONLY asSeq asSet

DATA ideSet == @(! : set[ide,<])

DEF {} 		== @({})
DEF {}?(s)	== {}?(s!)
DEF incl(e,s)	== @(incl(e,s!))
DEF excl(e,s)	== @(excl(e,s!))
DEF %(e)	== @(%(e))
DEF in(e,s)	== e in (s!)
DEF s1+s2	== @((s1!)+(s2!))
DEF s1-s2	== @((s1!)-(s2!))
DEF s1*s2	== @((s1!)*(s2!))
DEF arb(s)	== arb(s!)
DEF arbex(s)	== (arb(s),excl(arb(s),s))
DEF #(s)	== #(s!)
DEF s1=s2	== (s1!)=(s2!)
DEF s1<=s2	== (s1!)<=(s2!)
DEF s1<s2	== (s1!)<(s2!)

DEF (S1@)*-(S2@)== (@(i),@(d1),@(d2)) WHERE 
                   i == S1*S2
                   d1 == S1 - i
                   d2 == S2 - i              

DEF explode(s)  == asSeq(s!)
DEF implode(q)  == @(asSet(q))

DEF forall?(p,s) == forall?(p,s!)
DEF exist?(p,s) == exist?(p,s!)
DEF p|s         == @(p | (s!))

