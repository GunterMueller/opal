IMPLEMENTATION InterOpal
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date: 1999-10-06 21:22:31 $ ($Revision: 1.2 $)
-- %- =========================================================================
-- %- Autor:       wg, af, ralfi
-- %- Projekt:     OPAL1-Compiler - Zwischensprache InterOpal
-- %- subject: intermediate representation of OPAL
-- %- =========================================================================

IMPORT  Dyn             ONLY    dyn  
        Map             ONLY    map
        Nat             ONLY    10000 < >= max nat succ
        Option          ONLY    option
        Seq             ONLY    seq


DATA interOpal == 
        interOpal( part         : part,
                   structs      : map[origin,<,struct] 
                                -- Mapping from origin to main/imported
                                -- structure
                 )

DATA part ==
        signature
        extProperties
        implementation
        intProperties

                                
DATA struct == 
        struct(    pos          : seq[pos],
                                -- textual position(s) where struct
                                -- is imported (if any)
                   origin       : origin,
                                -- origin of this structure
                   symbol       : denotation,
                                -- symbolic identifier
                   insts        : map[inst,<,seq[name]],
                                -- instantiations
                                -- main =>
                                --    parameterized =>
                                --       exactly one formal instantiation 
                                --    else no instantiation 
                                -- NOT main =>
                                --    parameterized =>
                                --       all proper instantiations (inst(_))
                                --       + one formal instantiation 
                                --    else no instantiation
                   onlys        : map[inst,<,seq[name]],
                                -- onlys per instantiation
                                -- (imported structure only)
                   objs         : map[ident,<,object],
                                -- objects of this structure
                   props        : map[ident,<,property],
                                -- properties of this structure,
                   pragmas      : seq[pragma]
                                -- global pragmas of structure
        )

DATA property ==
        freeType(  pos          : pos,
                                  -- textual position of free DATA
                   ident        : ident,        
                                  -- identifier of property
                   sort         : name,
                                  -- sort associated with free DATA
                   variants     : seq[variant],
                                  -- variants of free DATA
                   pragmas      : seq[pragma]
                   -- %pragmas associated with property
        )
        law(       pos          : pos,
                                  -- textual position of law
                   ident        : ident,        
                                  -- identifier of property
                   symbol       : option[denotation],
                                  -- symbolic identifier of law (if any)
                   formula      : formula,
                   pragmas      : seq[pragma]
                   -- %pragmas associated with property
        )
        other(     pos: pos,
                   -- %textual position of law
                   id: denotation,
                   -- %symbolic name of kind of property
                   data: dyn,
                   -- %data associated with property
                   pragmas      : seq[pragma]
                   -- %pragmas associated with property 
              )
                  
DATA formula ==
        dfd(       pos          : pos,
                   expr         : expr
        )
        approx(    pos          : pos,
                   left         : expr,
                   right        : expr
        )
        cong(      pos          : pos,
                   left         : expr,
                   right        : expr
        )
        neg(       pos          : pos,
                   formula      : formula
        )
        conj(      pos          : pos,
                   left         : formula,
                   right        : formula
        )
        disj(      pos          : pos,
                   left         : formula,
                   right        : formula
        )
        impl(      pos          : pos,
                   premise      : formula,
                   conclusion   : formula
        )
        equiv(     pos          : pos,
                   left         : formula,
                   right        : formula
        )
        forall(    pos          : pos,
                   vars         : seq[var],
                   range        : formula
        )
        exist(     pos          : pos,
                   vars         : seq[var],
                   range        : formula
        )
        informal(  pos          : pos,
                   text         : denotation
        )
        other(     pos          : pos,
                   id           : denotation,
                   data         : dyn)
                   
                  
DATA object == 
        object(    pos          : seq[pos],
                                -- textual declaration position(s) (if any)
                   ident        : ident,
                                -- ident of this object
                   symbol       : denotation,
                                -- symbolic identifier
                   kind         : kind,
                                -- kind (sort or op with functionality)
                   def          : impl,
                                -- implementation
                   props        : seq[ident],
                                -- related properties (if any)
-- warum seq[ident] ??????????????????????????????
                   pragmas      : seq[pragma]
                                 -- related pragmas
        )

DATA impl == 
        missing         -- missing implementation 
        imported        -- implementation is hidden
        parameter( pos          : pos,
                                -- textual position in formal parameter list
                   no           : nat
                                -- logical position of parameter (1..n)
        )       
        induced(   pos          : pos,
                                -- textual position in DATA statement   
                   sort         : name
                                -- sort of DATA inducing implementation
        )
        data(      variants     : seq[variant] 
                                -- data definition
        )
        def(       eqns         : seq[eqn]
                                -- function definition
        )
        other(     pos          : pos,
                                -- textual position in formal parameter list
                   id           : denotation,
                                -- other information
                   data         : dyn
             )   

DATA variant == 
        variant(   pos          : pos,
                                -- variant position
                   cons         : name,
                                -- constructor function
                   test         : name,
                                -- test function
                   comps        : seq[comp]
                                -- components
       )

DATA comp == 
        comp(      pos          : pos,
                                -- component position
                   sel          : name
                                -- selector function
        )
                                
DATA eqn == 
        eqn(       pos          : pos,
                                -- equation position
                   lhs          : pattern,
                                -- original pattern
                   patterns     : seq[pattern], 
                                -- all perfect-fit patterns
                   rhs          : expr 
                                -- rhs of expression
        )

DATA pattern == 
        any(       pos          : pos
                                -- wildcard
        )
        name(      pos          : pos,
                   name         : name
                                -- globals only
        )
        var(       pos          : pos,
                   var          : var
        )
        as(        pos          : pos,
                   var          : var,
                   pattern      : pattern
        )
        apply(     pos          : pos,
                   funct        : pattern,
                   arg          : pattern
        )
        tuple(     pos          : pos,
                   elems        : seq[pattern]
        )

DATA var ==
        var(       pos          : pos,
                                -- declaration position
                   ident        : ident,
                                -- ident denoting variable
                   symbol       : denotation,
                                -- symbolic identifier
                   fct          : fct
                                -- functionality
        )

DATA expr == 
        name(      pos          : pos,
                   name         : name
        )
        den(       pos          : pos,
                   rep          : denotation
        )       
        tuple(     pos          : pos,
                   elems        : seq[expr] 
        )
        apply(     pos          : pos,
                   funct        : expr, 
                   arg          : expr 
        )
        cases(     pos          : pos,
                   guards       : seq[guard], 
                   else         : option[expr]
        )
        lambda(    pos          : pos,
                   vars         : seq[var],
                   body         : expr
        )
        block(     pos          : pos,
                   eqns         : seq[eqn],
                   body         : expr
        )

DATA guard == 
        guard(     pos          : pos,
                   if           : expr, 
                   then         : expr 
        )

DATA kind == 
        sort
        fct(       fct          : fct
        )
        other(     id           : denotation,
                   data         : dyn
        )

DATA fct == 
        sort(      sort         : name 
        )
        cart(      elems        : seq[fct] 
        )
        funct(     dom          : fct, 
                   codom        : fct 
        )

DATA pos  ==
        unknown
        part(       part                : part
        )
        pos(       part         : part,
                   row          : nat, 
                   col          : nat
        )
        region(    part         : part,
                   row          : nat,   -- start
                   col          : nat,   -- start
                   endRow       : nat,
                   endCol       : nat
        )

DATA name == 
        name(    ident          : ident,
                                -- ident unique w.r.t. origin if global,
                                -- ident unique w.r.t. associated global,
                 origin         : origin,
                 inst           : inst
        )

DATA ident ==
        ident(     no           : nat
        )
        
DATA origin ==
        local   -- origin of a local name
        main    -- origin of the main structure
        origin(    no           : nat
                                -- number unique w.r.t. interOpal
        )

DATA inst ==
        none    -- object/structure not parameterized
        formal  -- instantiated with own formal parameter
        generic
        inst(      no           : nat
                                -- number unique w.r.t. origin
        )


FUN < : inst ** inst -> bool
-- Ordnung: (none,formal,generic, 0,1,2,3,...)
DEF none        < none          == false
DEF none        < _             == true         -- formal, generic, inst
DEF formal      < _             == false        -- none, formal
DEF formal      < generic       == true
DEF formal      < inst(_)       == true
DEF generic     < _             == false        -- none, formal, generic
DEF generic     < inst (_)      == true
DEF inst(_)     < _             == false
DEF inst(N1)    < inst(N2)      == N1 < N2


FUN < : name ** name -> bool
-- DEF tmp < tmp        == false
-- DEF tmp < _  == true
-- DEF _   < tmp        == false
DEF name(Id1,O1,In1) < name(Id2,O2,In2) ==
        IF O1 < O2 THEN true
        IF O2 < O1 THEN false
        ELSE    IF Id1 < Id2 THEN true
                IF Id2 < Id1 THEN false
                ELSE In1 < In2 
                FI
        FI

FUN < : origin ** origin -> bool
-- Ordnung: (local,main,origin(0,1,2,3,...))
DEF local       < local         == false
DEF local       < _             == true         -- formal, inst
DEF main        < _             == false        -- none, formal
DEF main        < origin(_)     == true
DEF origin(_)   < _             == false
DEF origin(N1)  < origin(N2)    == N1 < N2

FUN < : ident ** ident -> bool
DEF ident(N1)   < ident(N2)     == N1 < N2

FUN firstLaw : ident
DEF firstLaw == ident(10000)

FUN nextLaw  : ident -> ident
DEF nextLaw(ident(N)) == ident(N succ)

FUN isLaw?   : ident -> bool
DEF isLaw?(ident(N)) == N >= 10000

DATA pragma == pragma( items: seq[pragmaItem] )

DATA pragmaItem == objects (objects: seq[name])
                   token   (pos: pos,
                            text: denotation) 


FUN ioTypeKey : denotation
DEF ioTypeKey == "interOpalKey2" 
