SIGNATURE Ic
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date: 1998-06-16 15:59:35 $ ($Revision: 1.1.1.1 $)

-- subject: The imperative code
-- author:  wg 1-92

IMPORT	Nat		ONLY nat <
     	Seq		[obj] ONLY seq
	Seq		[stm] ONLY seq
	Seq		[sel] ONLY seq
	Seq		[match] ONLY seq
	Seq		[ide] ONLY seq
	Set		[nat,<] ONLY set

IMPORT	NatMap		[mod] ONLY natMap
	NatMap		[obj] ONLY natMap

IMPORT 	Ide		ONLY ide origin:SORT
	Store		ONLY store
	SourceAn	ONLY pos:SORT errorKind

TYPE stm ==
        nop
	pos	(pos: pos)
	halt	(message: denotation, kind: errorKind)

	ass	(ins: seq[ide], outs: seq[ide])
	call	(ide: ide, ins: seq[ide], outs: seq[ide])
	eval	(ide: ide, ins: seq[ide], outs: seq[ide])

	inlineStatic  (macro: denotation, in: denotation, out: ide)
	inlineDynamic  (macro: denotation, in: ide, out: ide)

	copy	(ide: ide, store: store, times: nat)
	free	(ide: ide, store: store, times: nat)

        cons    (store: store, ins: seq[ide], out: ide)
        reuse	(store: store, cand: ide, candStore: store,
		 ins: seq[ide], out: ide)

	block	(vars: seq[obj], stms: seq[stm])
	case	(ide: ide, store: store, matches: seq[match], default: stm)

TYPE match ==
	match	(tags: set[nat,<], store: store, sels: seq[sel], stms: seq[stm])
	
TYPE sel ==
	dead	(ide: ide, store: store)
	plain	(ide: ide, store: store, times: nat)
	borrow	(ide: ide, store: store, times: nat)
	reuse	(ide: ide, store: store, times: nat)

TYPE obj ==
	variable 	(ide: ide, store: store)
	hidden 		(ide: ide, store: store, mode: mode, symbol: denotation)
	procedure	(ide: ide, store: store, mode: mode, symbol: denotation,
			 ins: seq[ide], outs: seq[ide],
		  	 stm: stm)
	constant	(ide: ide, store: store, mode: mode, symbol: denotation, 
			 init: stm)
	denotation	(ide: ide, store: store, mode: mode, symbol: denotation, 
			 repr: denotation)


TYPE mode ==
	external  internal  local

TYPE mod ==
	mod	(symbol: denotation, origin: origin, kind: modKind,
		 uses: natMap[mod], objs: natMap[obj])

TYPE modKind == normal foreign builtin
