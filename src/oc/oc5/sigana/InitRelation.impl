IMPLEMENTATION InitRelation

IMPORT 	
	Com	COMPLETELY
	ComCompose	COMPLETELY
	Error	COMPLETELY
	InterOpal	COMPLETELY
	OcBinStream	COMPLETELY
	Option	COMPLETELY
	Options	COMPLETELY
	Pair	COMPLETELY
	Pos	COMPLETELY
	Rep	COMPLETELY
	Struct	COMPLETELY
	Seq	COMPLETELY
	SeqMap	COMPLETELY
	SeqReduce ONLY \
	SeqOfSeq ONLY flat

FUN readOwn: options -> com[interOpal]

/* Von der eigenen Signatur muss der gesamte Namensraum rekonstruiert
werden. Das Mischen mehrerer eigener Strukturen - durch mehrfaches
REALIZES - wird noch nicht unterstützt.  */

DEF readOwn(Opt) ==
	read(Opt, mySignExpFile(Opt), ioTypeKey) 

FUN deps: struct'Struct -> seq[import]
DEF deps(Struct) == imports(Struct) 
			++ (2nd * relations(Struct))
		
FUN readInters:  options ** struct'Struct -> com[seq[interOpal]]
DEF readInters(Opt, Struct) ==
	LET
	    OrigSeq == \ (insertUnique, <>, deps(Struct))
	IN
	    readFiles(Opt, structIde(head(Struct)), OrigSeq)

FUN insertUnique : import ** seq[origin'Struct] -> seq[origin'Struct]
DEF insertUnique(I, <>) == origin(I) %
DEF insertUnique(I, S AS Ft :: Rt) ==
	LET Orig == origin(I) IN
	IF structIde(Orig) < structIde(Ft) THEN Orig :: S
	IF structIde(Orig) = structIde(Ft) THEN S
	ELSE Ft :: insertUnique(I, Rt) FI

FUN readFiles:  options ** rep ** seq[origin'Struct] 
					-> com[seq[interOpal]]
DEF readFiles(_, _, <>) == succeed(<>)
DEF readFiles(Opt, MainRep, Orig :: Rt) ==
	LET ReadRtFiles == readFiles(Opt, MainRep, Rt) 
	    ImpRep == structIde(Orig)
	    Pos == pos(Orig)
	IN
	IF MainRep = ImpRep THEN
		errors(Opt, err(error, 
			"illegal reference of current structure '%s'",
					`(MainRep), Pos)) & (\\ _ .
		ReadRtFiles)
	ELSE
	    LET ImpFile ==  expFile(`(ImpRep), Opt) 
	    IN optread(Opt, ImpFile, ioTypeKey) & (\\Inter?.
	    	IF nil?(Inter?) THEN 
	    		errors(Opt, err(error, "imported file '%s' not found", 
					ImpFile, Pos)) & (\\ _ .
			ReadRtFiles)
	    	ELSE ReadRtFiles & (\\InterSeq.
			succeed(cont(Inter?)::InterSeq))
	    	FI)
	FI
	
