
IMPLEMENTATION PForm
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/doc/LICENSE or
      http://projects.uebb.tu-berlin.de/opal/trac/wiki/License for details
*/
-- % $Date$ ($Revision$)

-- author: cg
-- purpose: parsing Opal1 and property language

IMPORT  -- pars
        PName           ONLY pName  pLocalName pIde pLawName 
        PExpr           ONLY pExpr pTuple pREq mkNames
        Token           ONLY token terminal lexem pos ` =  printTS
        Terminal        COMPLETELY
        PErr            COMPLETELY
        BaseParser      COMPLETELY
        PTypes          COMPLETELY
        ZOpts           COMPLETELY
        ModBase         COMPLETELY
        ModExpr         COMPLETELY
        ModMix          COMPLETELY
        ModForm         COMPLETELY
IMPORT  -- reps
        Rep             COMPLETELY
IMPORT  -- absy
        Struct          COMPLETELY
        Absy            COMPLETELY
IMPORT  -- admin
        OcVerbose         COMPLETELY
        OcPos             ONLY pos firstPos
        OcError           COMPLETELY
        OcOptions         COMPLETELY
IMPORT  -- lib
        Nat             ONLY nat
        Denotation      COMPLETELY
        Seq             COMPLETELY
        SeqReduce       COMPLETELY
        SeqMap          COMPLETELY
	String		COMPLETELY
        DEBUG           COMPLETELY
        PartName        COMPLETELY
        Impl            COMPLETELY
        Option          ONLY avail nil
        BoolConv        ONLY `

-- %- -------------------------------------------------------------------------

-- %$Outermost Calling Interface$
FUN pFormula pAtomForm pQuantForm pAndForm pOrForm pImplyForm pEqvForm
    pBrForm pNotForm : parser

-- % @code{Formula ::= EqvForm}
DEF pFormula   == pEqvForm

-- %$Propositional Formulae$
-- %@code{EqvForm ::= ImplyForm [ '<=>' (EqvForm | Formula) ]}
DEF pEqvForm   == parser(\\Arg.(\\ .
   pForm(pImplyForm,(isA(eqv)<&>(pEqvForm<|>pFormula))(modEqv),eqv!)
   @ Arg)())

-- %@code{ImplyForm ::= OrForm [ '==>' (ImplyForm | Formula) ]}
DEF pImplyForm == parser(\\Arg.(\\ .
    pForm(pOrForm,(isA(implies)<&>(pImplyForm<|>pFormula))(modImply),implies!) 
    @ Arg)())

-- %@code{OrForm ::= AndForm [ 'OR' (OrForm | Formula) ]}
DEF pOrForm    == parser(\\Arg.(\\ .
    pForm(pAndForm,(isA(or) <&> (pOrForm <|> pFormula))(modOr),or!)
    @ Arg)())

-- %@code{AndForm ::= NotForm [ 'AND' (AndForm | Formula) ]}
DEF pAndForm   == parser(\\Arg.(\\ .
    pForm(pNotForm,(isA(and)<&>(pAndForm <|> pFormula))(modAnd),and!)
    @ Arg)())

-- %@code{NotForm ::=  ('NOT' (NotForm | Formula)) | QuantForm | AtomForm}
DEF pNotForm == parser(\\Arg.(\\ .
    LET Pars  == IF not = ft(Arg)
                 THEN (isA(not) <&> (pNotForm <|> pFormula))(modNeg)
                 ELSE (pQuantForm <|> pAtomForm)  FI
    IN Pars @ Arg)())

/* %Auxiliary function @code{pForm(Left, Right, Op)} parses
    @code{Left [ 'Op' Right]} */
FUN pForm : parser ** parser ** terminal -> parser
DEF pForm (LP,RP,Term) == 
	(LP <&?> (isA(Term) <&.> RP))(modProp)

-- %Atomic Formula
DEF pAtomForm == 
	(pBrForm <|> (isA(dfd) <&> pExprF)(modDfd)) <|>
    	((pExprF <&?> ((isA(equal) <&> pExprF)(modEqu) <|>
                                (isA(ineq)  <&> pExprF)(modIneq)))
                  (modEqual))

DEF pBrForm  == (isA(openBr) <&.> pFormula) <.&> isA(closeBr) 

FUN pExprF: parser
DEF pExprF == 
    pExpr(stdMix)(pSet)

-- %Quantified Formula
DEF pQuantForm == parser(\\Arg.
    LET Pars == ((mkNames(pLocalName+) <.&> isA(dot)) <&> pFormula)
	(modQuant)
    IN (isA(all) <&> Pars)(modAllQuant) <|> (isA(ex) <&> Pars)(modExQuant)
	@ Arg)

-- %- ---------------------------------------------------------------------
/* %Terminals which may follow an expression used in formula:
    @code{),<<=,  ===, AND, OR< ==> <=> POST PRE} */ 
-- %- --------------------------------------------------------------------
FUN pSet : terminal -> bool
DEF pSet(T) == 
IF closeBr = T THEN true
IF ineq = T THEN true
IF equal = T THEN true
IF and = T THEN true
IF or = T THEN true
IF implies = T THEN true
IF post = T THEN true
IF pre = T THEN true
ELSE eqv = T FI
             
FUN pProofBody pProofHead: parser
-- %$Proofs$
DEF pProof == pProofBody <|> pProofHead


/* %@code{(pName <&> (isA(equation)) <&.> pExpr(Mix?))(modProofBody)}
  waere schoener, aber Fehlerpropagierung ist mangelhaft */
DEF pProofBody == 
   parser(\\ Arg.
   LET Res1 == pName @ Arg
   IN
   IF pFail?(Res1) THEN Res1
   ELSE
     LET Res2 == isA(equation) @ tokS(Res1)
     IN
     IF pFail?(Res2) THEN Res2
     ELSE
       LET Res3 == pExpr(stdMix) @ tokS(Res2)
       IN
       IF pRes?(Res3) THEN
         pRes(err(Res1) + err(Res2) + err(Res3),
              rep(modProofBody)(absy(Res1), absy(Res3)),
              tokS(Res3)
             )
       IF pFail?(Res3) THEN
         pFail(err(Res1) + err(Res2) + err(Res3),
               rep(modProofBody)(absy(Res1), absy(Res3)),
               tokS(Res3)
              )
       FI
     FI
   FI
   )


FUN modProofBody: modifier
DEF modProofBody == 
  mod(\\ name, script.
  IF Name?(name) and Expr?(script) THEN
    ProofBody(proofBody(pName(name), e(script), pos(pName(name))))
  ELSE
    PRINT(true, \\ . "modProofBody:\n" ++
          `(name) ++ "\n" ++ `(script), noAbsy)
--    noAbsy
  FI
  )
    
DEF pProofHead ==
  ((((pIde <.&> isA(colon)) <&>
     mkNames(*(pLawName)(turnstile = _)))
    (modProofHead1) <.&> isA(turnstile)) <&>
    (pLawName))
  (modProofHead2)

FUN modProofHead1 modProofHead2: modifier
DEF modProofHead1 ==
  mod(\\ pname, prems.
  IF Name?(pname) and NameSeq?(prems) THEN
    ProofHead(proofHead(ide(pName(pname)), nSeq(prems), nilName,
                        pos(pName(pname))))
  IF Name?(pname) and noAbsy?(prems) THEN
    ProofHead(proofHead(ide(pName(pname)), <>, nilName,
                        pos(pName(pname))))
  ELSE
--    PRINT(true, \\ . "modProofHead1:\n" ++
--          `(pname) ++ "\n" ++ `(prems), noAbsy)
    noAbsy
  FI
  )

DEF modProofHead2 ==
  mod(\\pH, concl.
  IF ProofHead?(pH) and Name?(concl) THEN
    ProofHead(proofHead(ide(ph(pH)), prems(ph(pH)), pName(concl), pos(ph(pH))))
  ELSE
--    PRINT(true, \\ . "modProofHead2:\n" ++
--          `(pH) ++ "\n" ++ `(concl), noAbsy)
    noAbsy
  FI
)

-- %Spezifikation:  SPC eq PRE formula POST formula
DEF pSpec ==
  parser(\\ Arg. 
    LET Res1a == (isA(spc) <&.>  pREq(stdMix)) @ Arg
        Res1 == PRINT(FIELD?[bool]("-zpSpec"), \\ . `(Res1a), Res1a)
    IN
    IF pFail?(Res1) THEN Res1
    ELSE
      LET Res2a == ((isA(pre) <&.> pFormula) <|> nil) @ tokS(Res1)
        Res2 == PRINT(FIELD?[bool]("-zpSpec"), \\ . `(Res2a), Res2a)
      IN
      LET Res3a == (isA(post) <&.> pFormula) @ tokS(Res2)
          Res3 == PRINT(FIELD?[bool]("-zpSpec"), \\ . `(Res3a), Res3a)
          nErr == err(Res1) + err(Res2) + err(Res3) + nErr4
          nTokS == tokS(Res3)
          (nAbsy, nErr4) == mkSpec(absy(Res1), absy(Res2), absy(Res3))
      IN
      (IF pRes?(Res3) THEN pRes ELSE pFail FI)(nErr, nAbsy, nTokS)
    FI
  )

FUN mkSpec: absy ** absy ** absy -> absy ** err
DEF mkSpec(EqSeq(eq :: R), Formula(pre), Formula(post)) ==
  (Spc(spc(eq, avail(pre), post, pos(eq))),
   (IF R <>? THEN ok
   ELSE err(error, "only one equation in specification", pos(ft(R)))
   FI) +
   (IF checkLhs(left(eq)) THEN ok
    ELSE err(error, "improper lhs of spc", pos(left(eq)))
    FI)
  )
DEF mkSpec(EqSeq(eq :: R), noAbsy, Formula(post)) ==
  (Spc(spc(eq, nil, post, pos(eq))),
   (IF R <>? THEN ok
   ELSE err(error, "only one equation in specification", pos(ft(R)))
   FI) +
   (IF checkLhs(left(eq)) THEN ok
    ELSE err(error, "improper lhs of spc", pos(left(eq)))
    FI)
  )
DEF mkSpec(A1, A2, A3) ==
  LET A == PRINT(true, \\ . "Eq is :" ++ `(A1) ++ ":\n" ++
                            "Pre is:" ++ `(A2) ++ ":\n" ++
                            "Post is:" ++ `(A3) ++ ":",
                  noAbsy)
  IN
  (A, err(error, "cannot construct specification", firstPos))

/* %erlaubte Syntax für linke Seite einer Spec: val, oder apply, wobei
    funct auch ok sein muss, arg muss ein tupel von vals sein. */
FUN checkLhs: expr -> bool
DEF checkLhs(val(_,_)) == true
DEF checkLhs(apply(f, a0, _)) == 
  LET a == PRINT(false, \\ . "checkLhs:t?" ++ `(tuple?(a0)) ++
                            " v:" ++ `(val?(a0)), a0)
  IN 
  IF tuple?(a) THEN forall?(val?, elems(a)) and checkLhs(f)
  IF val?(a) THEN checkLhs(f)
  ELSE false
  FI
DEF checkLhs(typed(e, _, _)) == checkLhs(e)
DEF checkLhs(e) == 
  PRINT(false, \\ . "checkLHS/else:" ++ `(e) , false)



FUN stdMix : modifier
DEF stdMix == mod1st
-- IF mix? THEN mkMix ELSE mkRhsAssoc  FI
