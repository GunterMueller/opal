
IMPLEMENTATION Parser
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date: 2000-04-14 23:43:01 $ ($Revision: 1.3 $)

-- author:      cg
-- purpose:     parsing

IMPORT  -- pars
        ModBody         COMPLETELY
        ModBase         COMPLETELY
        PName           COMPLETELY
        PForm           COMPLETELY
        ModForm         COMPLETELY
        PExpr           COMPLETELY
        ModExpr         COMPLETELY
        ModMix          COMPLETELY
        PCommon         COMPLETELY
        Token           COMPLETELY
        Terminal        COMPLETELY
        PErr            COMPLETELY
        BaseParser      COMPLETELY
        PTypes          COMPLETELY
        ZOpts           COMPLETELY
        PostParser      COMPLETELY
IMPORT  -- reps
        Rep             COMPLETELY
IMPORT  -- absy
        Struct          COMPLETELY
        Absy            COMPLETELY
        PartName ONLY sort? ` partName partName? pos op? nilName
	Impl		ONLY variant comp
IMPORT  -- admin
        OcVerbose         COMPLETELY
        OcPos             ONLY pos  `
        OcError           ONLY errors err ok errKind + error warning
        OcOptions         COMPLETELY
        OcExit          ONLY languageError
IMPORT  -- lib
        Nat             COMPLETELY
        NatConv         ONLY     `
        Denotation      COMPLETELY
        Com             COMPLETELY
        ComCompose      COMPLETELY
        Seq             COMPLETELY
        SeqConv         COMPLETELY
        SeqFilter       COMPLETELY
        SeqIndex        COMPLETELY
	Stream		ONLY output stdOut writeLine
        Pair            COMPLETELY
	Void		COMPLETELY
        DEBUG           ONLY PRINT FIELD?

-- %- ------------------------------------------------------------------------

FUN initAbsy : options ** seq[token] -> denotation ** parser ** absy
DEF initAbsy(Options, tokens) ==
	LET SName == IF (take(2,tokens) #) = 2 THEN `(lexem(tokens ! 1))
                 		ELSE "" FI 
	IN 
	IF sign?(Options) THEN 
	  IF ::?(tokens) ANDIF theory = ft(tokens) THEN
		("Theory " ++ SName, isA(theory), mkInitT)
	  ELSE ("Signature " ++ SName, isA(signature), mkInitS) FI
	ELSE ("Implementation " ++ SName, isA(implementation), mkInitI)
	FI

DEF parse(Options)(tokens & repTab) ==
LET (Text, Key, IAbsy) == initAbsy(Options, tokens) IN
    verb(Options,subphase,"parsing " ++ Text ++ " ...") & (\\ _ .
    succeed(prepareTS(tokens)) & (\\PrepTs.
    (IF cop?("printPrepTS", Options) THEN
	writeLine(stdOut, prntTSS(PrepTs))
    ELSE succeed(nil) FI) & (\\ _ .   	
    parse(Options, PrepTs)(Key, IAbsy) & (\\Absy.
    succeed(postParser(structOf(Absy) & repTab)) & (\\StructTab.
    (IF cop?("printFinAbsy", Options) THEN
	writeLine(stdOut, `(1st(StructTab)))
    ELSE succeed(nil) FI) & (\\ _ .   	
    (IF cop?("printRepTab", Options) THEN
	writeLine(stdOut, dump(2nd(StructTab)))
    ELSE succeed(nil) FI) & (\\ _ .
    succeed(StructTab)
    )))))))


FUN prntTSS : seq[seq[token]] -> denotation
DEF prntTSS(TSS) == `("", "\n", "\n")(printTS)(TSS)


-- %- ------------------------------------------------------------------------

FUN parse: options ** seq[seq[token]] -> parser ** absy -> com[absy]
DEF parse(Opts,HeadS::BodyS)(Head,Init)==  
    pHead(Opts)(Head)(HeadS) & (\\ ResH.
    pBody(Opts)(BodyS,pRes(ok,Init,<>),1st(ResH)) & (\\ResB. 
    LET Absy == absy(modify(2nd(ResH),2nd(ResB),modStructOri))
    IN (IF cop?("printAbsy", Opts) THEN
	writeLine(stdOut, `(Absy))
	ELSE succeed(nil) FI) & (\\ _ .
    succeed(Absy)
    )))


DEF parse(Opts,<>)(Head,Init) == 
    pHead(Opts)(Head)(<>) & (\\ _ . succeed(Init))

-- %- ------------------------------------------------------------------------
-- Argument parser of pHead parses implementation or signature resp.

FUN pHead : options -> parser -> seq[token] -> com[pair[bool,pRes]]
DEF pHead(Opts)(P)(Arg) == 
    LET Name    == IF extp?(Opts) ORIF intp?(Opts) THEN first(drop(2,Arg))
                   ELSE first(rest(Arg))         FI
        NameErr == IF `(lexem(Name)) = myStruct(Opts) THEN ok
                   ELSE pErrExpFN(myStruct(Opts),Name)    FI
        Head    == ((P <&.> pOrigin) <.&> empty(ftBody)) @ Arg
    IN errors(Opts,err(Head) + NameErr) & (\\ ok? . 
	yield(okay(ok? & Head)))

-- %- -------------------------------------------------------------------------

FUN pBody : options -> seq[seq[token]] ** pRes ** bool -> com[pair[bool,pRes]]

DEF pBody(_)   (<>,SoFar,Ok?)              ==  yield(okay(Ok? & SoFar))
DEF pBody(Opts)((Tok::One)::RTS,SoFar,Ok?) == 
    parsBody(ftBody)(Opts,Tok)(One) & 
       (\\Res. LET More == modify(SoFar,2nd(Res),modStructBody)
               IN pBody(Opts)(RTS,More,1st(Res) and Ok?))

FUN parsBody: (terminal -> bool) -> options**token -> seq[token] 
						-> com[pair[bool,pRes]]
DEF parsBody(_)(Opts,Tok)(<>) == 
    IF terminal(Tok) = eof 
    THEN yield(okay(true & pRes(ok,noAbsy,<>)))
    ELSE errors(Opts,pErrMiss("Body of " ++ `(terminal(Tok)),missPos(Tok))) 
           & (\\ _ . yield(okay(false & okRes)))
    FI
DEF parsBody(FtSet)(Opts,Tok)(TS AS T:: _) == 
    IF terminal(T) = eof 
    THEN errors(Opts,pErrMiss("Body of " ++ `(terminal(Tok)),missPos(Tok))) 
           & (\\ _ . yield(okay(false & okRes)))
    ELSE IF def = Tok    THEN pDef(Opts)(TS)
         IF fun = Tok    THEN pFun(Opts)(TS)
         IF sort = Tok   THEN pSort(Opts)(TS)
         IF data = Tok   THEN pType(Opts, data)(TS)
         IF type = Tok   THEN pType(Opts, type)(TS)
         IF import = Tok THEN pRelation(Opts, import)(TS)
         IF assert = Tok THEN pRelation(Opts, assert)(TS)
         IF assume = Tok THEN pRelation(Opts, assume)(TS)
         IF law = Tok    THEN pLaw(Opts)(TS)
         IF prBeg = Tok  THEN pPragma(Opts)(Tok::TS)
         IF proof = Tok  THEN pProof(Opts)(TS)
         IF terminal(Tok) = eof    THEN parsBody(FtSet)(Opts,Tok)(rt(TS))
         ELSE errors(Opts,pErrExp(FtSet)(Tok,pos(Tok)))
            & (\\ _ . yield(okay(false & okRes)))
    FI   FI

-- %- ------------------------------------------------------------------------
-- <Relation> origin ((ONLY | ALLBUT) name1 ... nameN | [ COMPLETELY ]) +
-- %- ------------------------------------------------------------------------
FUN pRelation : options ** rep -> seq[token] -> com[pair[bool,pRes]]
DEF pRelation(Opts, Type)(Tok::TS) ==
  verb(Opts,definition,"parsing " ++ `(Type) ++
                       " " ++ `(lexem(Tok)) ++ " ...") & (\\ _ . 
  LET Relation == (pRelation(Opts, Type) <.&> empty) @ (Tok::TS) IN 
  errors(Opts,err(Relation)) & (\\ ok? . 
  succeed(ok? & Relation)))

FUN pRelation : options ** rep -> parser
DEF pRelation (_, Type) == 
    LET Allbut? == \\T. IF allbut = T ORIF only = T THEN true 
			ELSE completely = T FI
    IN
    (pOrigin <&>  (     isA(completely)
                        <|> (isA(allbut) <&> +(pName))(modButImport)
                        <|> (isA(only) <&> +(pName))(modOnlyImport)
			<|> empty
                        <|> fail(pErrExp(Allbut?))))
                     (modRelation(Type))
    
/* %Hier könnte man je nach Art der Relation auch nur ONLY pOnlyOnly oder
    nur COMPLETELY zulassen. (kd) */

-- %- -----------------------------------------------------------------------
-- 'FUN' (Ide + ':' fct) + [ spec ]
-- %- -----------------------------------------------------------------------

FUN pFun : options -> seq[token] -> com[pair[bool,pRes]]
DEF pFun(Opts)(Tok::TS) ==
  verb(Opts,definition,"parsing FUN " ++ `(lexem(Tok)) ++ " ...") & (\\ _ . 
      -- % erst mal eine Deklarationsliste parsen
      LET Fun == pFun @ (Tok::TS)
      IN 
      -- % Programmstück zu Ende oder @code{FUN} fehlerhaft -> zurück
      IF (terminal(ft(tokS(Fun))) = eof) ORIF (Fun pFail?) THEN
        errors(Opts,err(Fun)) & (\\ ok?.
        succeed(ok? & Fun))
      -- % Spezifikation folgt
      IF spc = ft(tokS(Fun)) THEN
        LET Spec == pSpec @ tokS(Fun)
            nErr ==   IF <>?(rt(nSeq(absy(Fun)))) THEN ok 
                      ELSE err(error, 
                               "specification for single declarations only",
                               pos(Tok))
                      FI
        IN
        errors(Opts, nErr + err(Spec)) & ( \\ ok? .
        succeed(ok? & modify(Fun, Spec, modSpec))
        )
      -- %   weitere Deklarationslistes nach demselben @code{FUN} parsen
      ELSE 
        pFun(Opts)(tokS(Fun)) & (\\NFun. 
        succeed(1st(NFun) & modify(Fun,2nd(NFun),modPNSeq))
        )
      FI)

-- %- ------------------------------------------------------------------
/* % @code{pFun} should have been defined as 
@w{@code{DEF pFun == (((pIde+)) <&> (isA(colon) <&.> pFct))(modFun)}}
but the context analysis expects the parser to check the context condition
"compound object". 

This should be changed one day in order to get a clean seperation between 
the destination of parsing and checking ... (cg)
*/
-- %- -------------------------------------------------------------------------

FUN pFun : parser
DEF pFun == ((pIde+) <&> pFctKind)(modFun)

-- %- -----------------------------------------------------------------------
-- 'SORT' Ide+
-- %- -----------------------------------------------------------------------
FUN pSort : options -> seq[token] -> com[pair[bool,pRes]]
DEF pSort(Opts)(Tok::TS) ==
  verb(Opts,definition,"parsing SORT " ++ `(lexem(Tok)) ++ " ...") &
  (\\ _ . LET Sort == (updAbsy(mkSort)(pIde+) <.&> empty) @ (Tok::TS)
          IN errors(Opts,err(Sort)) & (\\ ok? . 
		yield(okay(ok? & Sort))))


-- %- -------------------------------------------------------------------------
-- ('DATA' | 'TYPE') Name '==' ...
-- %- -------------------------------------------------------------------------

FUN pType : options ** rep -> seq[token] -> com[pair[bool,pRes]]
DEF pType(Opts, Key)(Tok::TS) ==
verb(Opts,definition,"parsing " ++ `(Key) ++ " " ++ `(lexem(Tok)) ++ " ...") &
  (\\ _ . LET Data == IF data = Key THEN updAbsy(mkData)(pDataType) 
			ELSE pDataType FI
	      Type == Data @ (Tok::TS)
          IN errors(Opts,err(Type)) 
             & (\\ ok? . yield(okay(ok? & Type))))

-- %- ------------------------------------------------------------------------

FUN pDataType :  parser
DEF pDataType == 
    ((pIde <.&> empty(equation = _)) <&> 
	((isA(equation)<&.>(updAbsy(flattenVariants)(pVariant +)))
                <.&> empty))(modType)

FUN pVariant pComps: parser
DEF pVariant == (updAbsy(\\A. LET N == pName(A) IN 
	Variants(variant(N,nilName,<>,pos(N))%))(pIde) <&?> pComps)(modVariant)

DEF pComps   == 
    isA(openBr) <&.> ((updAbsy(flattenComps)(pNameOrFct || !(comma))) 
			<.&> isA(closeBr))

-- %- ------------------------------------------------------------------------
-- DEF lhs  == rhs
-- %- ------------------------------------------------------------------------

FUN pDef : options -> seq[token] -> com[pair[bool,pRes]]
DEF pDef(Opts)(TS) ==
  verb(Opts,definition,"parsing DEF " ++ `(lexem(first(TS))) ++ " ...") &
  (\\ _ . 
   LET  (MixLhs?,Mix?)  == -- IF mix? THEN 
			(mkMix,mkMix)
                           -- ELSE (mkLhsNoMix,mkRhsAssoc) FI
        DefP   == (pDef(MixLhs?,Mix?)) @ TS
        Def    == PRINT(traceParse?,
                        \\ . "Result of Definition " ++ `(DefP), DefP)
  IN errors(Opts,err(Def)) & (\\ ok? .   
   yield(okay(ok? & Def))))


-- %- ------------------------------------------------------------------------

FUN pDef : modifier ** modifier -> parser
-- mit section auf tuple und _
DEF pDef(MLhs?,M?) == parser(\\ Arg. 
    LET Lhs == (IF equation = ft(Arg) THEN fail(missLhs) 
                    ELSE pTopLhs(MLhs?)  FI) @ Arg
        up_eq == isA(equation) @ tokS(Lhs)
        Rhs   == modify(up_eq,postMod(mkTopSec)(pExpr(M?)) @ tokS(up_eq),modEq)
    IN modify(Lhs,Rhs,modEq))


FUN pTopLhs : modifier -> parser
DEF pTopLhs(M) == 
    (pLhs(M)(top) <.&> empty(equation = _))


FUN pLhs : modifier -> (terminal -> bool) -> parser
DEF pLhs(M)(FtSet) == parser(\\Arg.
    -- doMod(M, 
    (empty(FtSet) <&.> pLhsExprS(M)(FtSet)) <|> fail(errKey) -- mkTopSec)
	@ Arg)

FUN mkMix : modifier
DEF mkMix == mod1st
FUN pLhsExprS : modifier -> (terminal -> bool) -> parser 
DEF pLhsExprS (M)(F) ==
    LET InnerMod == -- IF mix? THEN 
                    mkMix
                    -- ELSE mkInnerLhsAssoc FI
    IN  updAbsy(mkMixExpr)(((pLhsExpr(M)(F)) 
         <|> (pTuple(pLhs(InnerMod)(inside))))
         + ) 


FUN pLhsExpr : modifier -> (terminal -> bool) -> parser 
DEF pLhsExpr(M)(F) == parser(\\Arg. 
    LET LhsName == PRINT(traceP?,
                        \\ . `(pLocalName @ Arg),
                        mkExpr(pLocalName) @ Arg)
    IN IF !(as) F THEN 
          IF as = ft(tokS(LhsName)) THEN 
             modify(LhsName,
                   (isA(as) <&.> pLhs(M)(F)) @ tokS(LhsName),modAsE)
          ELSE LhsName
          FI
       ELSE LhsName
       FI)

FUN errKey : seq[token] -> err 
DEF errKey == \\Arg. pErrUnExp(top)(ft(Arg)%)

FUN top inside : terminal -> bool
DEF top(T) == 
IF ide = T THEN true
IF ignPat = T THEN true
ELSE openBr = T FI

DEF inside(T) == IF top(T) THEN true ELSE as = T FI

-- %- -------------------------------------------------------------------------
-- %$Laws$
-- %@code{LAW ==> [ Ide '==' ] Formula}
FUN pLaw : options -> seq[token] -> com[pair[bool,pRes]]
DEF pLaw(Opts)(Tok::TS) ==
  verb(Opts,definition,"parsing LAW " ++ `(lexem(Tok)) ++ " ...") & (\\ _ . 
     LET P == IF equation = ft(TS) THEN 
                  ((pIde <.&> isA(equation)) <&> pFormula)(modLaw(pos(ft(TS))))
              ELSE (pFormula <&> nil)(modLaw(pos(Tok)))
              FI
         Law  == (P <.&> empty) @ (Tok::TS)
     IN 
  errors(Opts,err(Law)) & (\\ ok? . 
  yield(okay(ok? & Law))))

-- %- ------------------------------------------------------------------------
FUN pPragma : options -> seq[token] -> com[pair[bool,pRes]]

DEF pPragma(Opts)(Tok::TS) == 
    verb(Opts,definition,"parsing PRAGMA in Position " ++ `(pos(Tok))) &
    (\\ _ . 
        LET PragP == ((pPragItem <.&> isA(prEnd)) <.&> empty) @ TS
            Prag == PRINT(tracePrag?, \\ . "Pragma " ++ `(PragP),PragP)
        IN 
    errors(Opts,err(Prag)) & (\\ ok? . 
    yield(okay(ok? & Prag))))

-- %@zoindex tracePrag
FUN tracePrag? : bool
DEF tracePrag? == FIELD?[bool]("-ztracePrag")

FUN pPragItem : parser
DEF pPragItem == 
 updAbsy(flattenGPragma)(((isA?(openSqBr!) <&.> updAbsy(mkPItem)(pParams)) <|> 
	updAbsy(flattenGPragma)(+(isIn(mkPrTokAbsy, prLexemSet)))) *)

FUN prLexemSet: terminal -> bool
DEF prLexemSet(T) == 
IF prBeg = T THEN false
IF prEnd = T THEN false
IF openSqBr = T THEN false
IF closeSqBr = T THEN false
IF eof = T THEN false
ELSE true FI

-- %- -------------------------------------------------------------------------
-- % 'PROOF' (( Ide ':' partName* '|-' partName ) | (partName '==' expr))

FUN pProof: options -> seq[token] -> com[pair[bool, pRes]]
DEF pProof(Opts)(T AS Tok:: _) ==
  verb(Opts, definition, "parsing PROOF "++ `(lexem(Tok)) ++ " ...") & (\\ _ .
  LET Proof == pProof @ T
  IN
    errors(Opts, err(Proof)) & (\\ok? .
    succeed(ok? & Proof)))


