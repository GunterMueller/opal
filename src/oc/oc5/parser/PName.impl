
IMPLEMENTATION PName
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date$ ($Revision$)

-- %$author: cg$
-- %$purpose: parsing Opal1 and property language$

IMPORT    -- pars
          PErr                  COMPLETELY
          Terminal              COMPLETELY
          Token                 COMPLETELY
          ZOpts                 COMPLETELY
          PTypes                COMPLETELY
          ModBase               COMPLETELY
          BaseParser            COMPLETELY
IMPORT    -- absy
          Absy                  COMPLETELY
	  Struct		COMPLETELY
          PartName              COMPLETELY
IMPORT    -- reps               
          Rep                   COMPLETELY
IMPORT    -- admin
          OcPos                   ONLY pos unknown +
          OcError 		ONLY warning errKind error err
IMPORT    -- lib
          Denotation            COMPLETELY
	  Option		COMPLETELY
	  Nat			COMPLETELY
          Seq	                COMPLETELY
          SeqMap                COMPLETELY
       	  String 		ONLY string

-- %- ----------------------------------------------------------------------
-- %$Opal specific base parsers for Names$
-- %- ----------------------------------------------------------------------

FUN pIde pFctName pName pLocalName pFct : parser

FUN mkIde: token -> absy
DEF mkIde(token(_, Lex, Pos)) == Name(partName(Lex,nil,nil,unknown,none,Pos))

DEF pIde      == isA(mkIde, ide) 

FUN pOri : parser
DEF pOri == isA(apo) <&.> pIde

FUN pNameFront :  parser
DEF pNameFront ==  isA(mkIde, ignPat) <|>
  (((pIde <&?> pOri)(modOrigin))  <&?> (pParams))(modParams)

DEF pFctName == (pNameFront  <&?> pFctKind)(modKind)

DEF pName      == (nameParser <.&> empty(specialInName))
DEF pLocalName == (pFctName <.&> empty(specialInName))

FUN nameParser :  parser
DEF nameParser == 
  	(pNameFront  <&?> pKind)(modKind)

DEF pOrigin == updAbsy(mkOrigin)((pIde <&?> pParams)(modParams))

FUN modOrigin: modifier
DEF modOrigin == mod(\\A1,A2.
		IF (A1 Name?) 
		THEN IF (A2 Name?) THEN
                      	Name(:=_structIde(pName(A1), avail(ide(pName(A2)))))
		      ELSE A1 FI
		ELSE A1 FI)  -- noAbsy

FUN mkOrigin: absy -> absy
DEF mkOrigin(A) == 
           LET N == pName(A) IN
		Origin(origin(ide(N),pos(N),paramS(N)))

-- %- ----------------------------------------------------------------------

FUN pParams : parser
DEF pParams  == 
   ((isA(openSqBr) <&> (pNameOrFct || !(comma)))(modOpenInstPos) 
	<&> isA(closeSqBr))(modCloseInstPos)

FUN modOpenInstPos modCloseInstPos modParams: modifier
DEF modOpenInstPos == mod(\\A1, A2.
	IF Pos?(A1) THEN Name(:=_instPos(:=_paramS(nilName, 
				avail(pName * aSeq(A2))),
					p(A1)))
	ELSE Name(nilName) FI)

DEF modCloseInstPos == mod(\\A1, A2.
	IF Pos?(A2) THEN Name(:=_instPos(pName(A1),
			instPos(pName(A1)) + p(A2)))
	ELSE A1 FI)

DEF modParams == mod(\\A1, A2.
	IF Name?(A1) 
	THEN IF Name?(A2) THEN
		LET P1 == pName(A1)
		    P2 == pName(A2)
		IN Name(:=_instPos(:=_paramS(P1, paramS(P2)), instPos(P2)))
	     ELSE A1 FI
	ELSE A2 FI)     

FUN pKind : parser
DEF pKind == isA(colon) <&.>
		(setAbsy(Kind(sort), isA(sort)) <|>
		 setAbsy(Kind(law), isA(law))  <|>
		 setAbsy(Kind(proof), isA(proof))  <|>
		 pKindFct)

FUN pKindFct: parser
DEF pKindFct == 
  updAbsy(\\N. IF Name?(N) THEN Kind(op(pName(N))) ELSE N FI)
	(pFct(true))

FUN pFctKind: parser
DEF pFctKind == isA(colon) <&.> pKindFct

FUN pSortName:parser 
DEF pSortName == updAbsy(mkKind(sort))(pNameFront) <.&?> 
	(isA(colon) <&.> isA(sort))

FUN pLawName:parser 
DEF pLawName == updAbsy(mkKind(law))(pNameFront) <.&?>
		(isA(colon) <&.> isA(sort))

FUN mkKind: kind -> absy -> absy
DEF mkKind(DefaultKind)(A) == 
	IF A Name? THEN Name(:=_kind(pName(A), DefaultKind))
	ELSE A FI

FUN modKind: modifier
DEF modKind == mod(\\A1,A2. 
 	IF (A1 Name?) ANDIF A2  Kind? THEN
		Name(:=_kind(pName(A1), kind(A2)))
	ELSE A1 FI)


-- %- ----------------------------------------------------------------------
-- %$Functionality Parser$
-- %- ----------------------------------------------------------------------
-- %- ------------------------------------------------------------------------
-- %$$Fct$
-- %Parses : @code{Fct ::= ProdFct [ '->' Fct]}
-- %- ------------------------------------------------------------------------

FUN pFct pNameOrFct : parser
DEF pNameOrFct == pFct(false)
DEF pFct == pFct(true)

FUN pFct pProdFct pSimpleFct: bool -> parser

DEF pFct(Sort?) == parser(\\Arg.
-- avoid empty functionalities
	isIn?(specialInFct) <&.>   
	((pProdFct(Sort?) <&?> (isA(arrow) <&.> pFct))(modFunFct))
	@ Arg)

FUN modFunFct : modifier
DEF modFunFct == mod(\\A1,A2. 
    IF Name?(A2) THEN
	LET F1 == pName(A1)
	    F2 == pName(A2)
	    P == pos(F1) + pos(F2) 
	IN Name(partName(arrow, nil, avail(%(F1, F2)), P, sort, P))
    ELSE A1 FI)

-- %- ------------------------------------------------------------------------
-- %$$ProdFct$
-- %Parses : @code{ProdFct ::= SimpleFct || ** }
-- %- ------------------------------------------------------------------------

DEF pProdFct(Sort?) == 
	updAbsy(mkProd) (pSimpleFct(Sort?) || !(product))  -- could be empty

FUN mkProd: absy -> absy
DEF mkProd(A) == LET S == pName * aSeq(A)
		     P == IF #(S) > 1 THEN
				pos(ft(S)) + pos(last(S))
			  ELSE unknown FI  
		 IN IF #(S) = 1 THEN Name(ft(S))
			 ELSE Name(partName(product, nil, avail(S),
				P, sort, P)) FI


-- %- ------------------------------------------------------------------------
-- %$$SimpleFct$
-- %Parses : @code{SimpleFct ::= '(' pFct ')' | Name  | '(' ')' | '_'}
-- %- ------------------------------------------------------------------------

DEF pSimpleFct(Sort?) == (((isA(openBr) <&.> 
	pFct(true)) <.&> isA(closeBr))) 
	<|> (IF Sort? THEN pSortName
		ELSE nameParser FI)

-- %- ----------------------------------------------------------------------

FUN specialInName : terminal -> bool
DEF specialInName(T) == IF T < (colon!) THEN true
			ELSE (apo!) < T FI

FUN specialInFct : terminal -> bool
DEF specialInFct(T) == 
IF T = ide THEN true
IF T = ignPat THEN true
IF openBr = T THEN true
ELSE closeBr = T FI		 


