IMPLEMENTATION LexScanPosition
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/doc/LICENSE or
      http://projects.uebb.tu-berlin.de/opal/trac/wiki/License for details
*/
-- % $Date: 2012-10-02 14:17:54 +0200 (Tue, 02 Oct 2012) $ ($Revision: 838 $)

IMPORT ABORT         COMPLETELY
       Nat           COMPLETELY
       PrettyPrinter COMPLETELY

DATA position == position(row:nat,col:nat)
                 unknown

DEF position(r,c) :=_row r1 == position(r1,c)
DEF unknown       :=_row r1 == ABORT(":=_row'ScannerPosition: cannot update unknown")
DEF position(r,c) :=_col c1 == position(r,c1)
DEF unknown       :=_col c1 == ABORT(":=_col'ScannerPosition: cannot update unknown")

DEF origin == position(1,1)

DEF advanceRow(pos) == advanceRow(1,pos)
DEF advanceCol(pos) == advanceCol(1,pos)

DEF advanceRow(n,position(r,c)) == position(r+n,col(origin))
DEF advanceRow(n,unknown)       == ABORT("advanceRow'ScannerPosition: cannot advance unknown")
DEF advanceCol(n,position(r,c)) == position(r,c+n)
DEF advanceCol(n,unknown)       == ABORT("advanceCol'ScannerPosition: cannot advance unknown")

DEF position(r1,c1) + position(r2,c2) == position(r1+r2,c1+c2)
DEF unknown         + position(r2,c2) == ABORT("+'ScannerPosition: left argument is unknown")
DEF position(r1,c1) + unknown         == ABORT("+'ScannerPosition: right argument is unknown")
DEF unknown         + unknown         ==  ABORT("+'ScannerPosition: both arguments unknown")

DEF position(r1,c1) - position(r2,c2) == position(r1-r2,c1-c2)
DEF unknown         - position(r2,c2) == ABORT("-'ScannerPosition: left argument is unknown")
DEF position(r1,c1) - unknown         == ABORT("-'ScannerPosition: right argument is unknown")
DEF unknown         - unknown         ==  ABORT("-'ScannerPosition: both arguments unknown")

DEF `(pos) == pretty(pp(pos))
DEF pp(position(r,c)) == num(r) <> colon <> num(c)
DEF pp(unknown)       == question


-- %@ignore
-- Local variables:
-- coding: latin-1-unix
-- mode: opal
-- End:
-- %@end ignore
