IMPLEMENTATION ParserLCompose
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/doc/LICENSE or
      http://projects.uebb.tu-berlin.de/opal/trac/wiki/License for details
*/
-- % $Date$ ($Revision$)

IMPORT ParserL COMPLETELY
       Option COMPLETELY
       Void ONLY void nil

DEF P1 & fP2 == asParser(\\s.
    LET (d, r) == asFct(P1)(s)
    IN IF d nil? THEN (nil, s)
       ELSE LET P2 == fP2(cont(d))
                (d2, r2) == asFct(P2)(r)
            IN IF d2 nil? THEN (nil, s)
               ELSE (d2, r2)
               FI
       FI)

DEF P1 ; fP2 == 
    asParser(\\s. LET (d, r) == asFct(P1)(s)
                       P2 == fP2(d)
                       (d2, r2) == asFct(P2)(r)
                  IN IF d2 nil? THEN (nil, s) ELSE (d2, r2) FI)

DEF P1 & (SEP, fP2) == asParser(\\s.
LET (d1, r1) == asFct(P1)(s)
IN IF d1 nil? THEN (nil, s)
   ELSE LET (d2, r2) == asFct(SEP)(r1)
        IN IF d2 nil? THEN (nil, s)
           ELSE LET P2 == fP2(cont(d1))
                    (d3, r3) == asFct(P2)(r2)
                IN IF d3 nil? THEN (nil, s)
                   ELSE (d3, r3)
   FI      FI      FI)                


DEF P1 ; (SEP, fP2) == asParser(\\s.
LET (d1, r1) == asFct(P1)(s)
IN IF d1 nil? THEN LET P2 == fP2(d1)
                       (d2, r2) == asFct(P2)(r1)
                   IN IF d2 nil? THEN (nil, s)
                      ELSE (d2, r2)
                      FI
   ELSE LET (d2, r2) == asFct(SEP)(r1)
        IN IF d2 nil? THEN (nil, s)
           ELSE LET P2 == fP2(d1)
                    (d3, r3) == asFct(P2)(r2)
                IN IF d3 nil? THEN (nil, s)
                   ELSE (d3, r3)
           FI      FI
   FI)


