IMPLEMENTATION FileSystem
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date: 2001-05-09 13:24:48 $ ($Revision: 1.3 $)
-- access to the file system

IMPORT
  Void                ONLY void nil
  Nat                 ONLY nat 0 1 2 3 4 5 6 7 < =
  Option              ONLY option nil avail
  Seq[denotation]     ONLY seq
  String              ONLY string !
  StringConv          ONLY `
  Array               ONLY array
  ArrayConv           ONLY asSeq
  Com                 ONLY com ans yield okay call
  ComCompose          ONLY &
  SysTime                ONLY time: SORT
  UserAndGroup        ONLY userid: SORT groupid: SORT

-- EXPORTED TYPES --
/$ handcoded [inode:SORT, device:SORT, filemode:SORT] $/

DATA filetype == regular
                 directory
                 charSpecial
                 blockSpecial
                 fifo
                 socket
                 symLink
                 unknown

DATA permission == setUIdOnExec
                   setGIdOnExec
                   stickyBit
                   ownerRead
                   ownerWrite
                   ownerExec
                   groupRead
                   groupWrite
                   groupExec
                   worldRead
                   worldWrite
                   worldExec

DATA filestat == filestat( type   : filetype,
                           mode   : filemode,
                           inode  : inode,
                           device : device,
                           nlinks : nat,
                           owner  : userid,
                           group  : groupid,
                           size   : nat,
                           atime  : time,
                           ctime  : time,
                           mtime  : time
                         )


-- INTERNAL TYPES --

-- EXPORTED FUNCTIONS --

FUN < = : inode ** inode -> bool
/$ handcoded [< : inode ** inode -> bool, = : inode ** inode -> bool] $/

FUN < = : device ** device -> bool
/$ handcoded [< : device ** device -> bool, = : device ** device -> bool] $/

FUN < : filetype ** filetype -> bool
DEF FType1 < FType2 == ftord(FType1) < ftord(FType2)

FUN = : filetype ** filetype -> bool
DEF FType1 = FType2 == ftord(FType1) = ftord(FType2)

FUN < = : filemode ** filemode -> bool
/$ handcoded [< : filemode ** filemode -> bool,
              = : filemode ** filemode -> bool] $/

FUN < : filestat ** filestat -> bool
DEF Stat1 < Stat2 ==
  (device(Stat1) < device(Stat2)) or
  ((device(Stat1) = device(Stat2)) and (inode(Stat1) < inode(Stat2)))

FUN = : filestat ** filestat -> bool
DEF Stat1 = Stat2 ==
  (device(Stat1) = device(Stat2)) and (inode(Stat1) = inode(Stat2))

FUN {} : filemode
DEF {} == hc_emptymode

FUN none? : filemode -> bool
DEF Mode none? == hc_nonemode(Mode)

FUN % : permission -> filemode
DEF Perm % == {} + Perm

FUN + : filemode ** permission -> filemode
DEF Mode + Perm == hc_addmode(Mode,Perm)

FUN - : filemode ** permission -> filemode
DEF Mode - Perm == hc_delmode(Mode,Perm)

FUN + : permission ** permission -> filemode
DEF Perm1 + Perm2 == %(Perm1) + Perm2

FUN + : filemode ** filemode -> filemode
DEF Mode1 + Mode2 == hc_ormode(Mode1,Mode2)

FUN - : filemode ** filemode -> filemode
DEF Mode1 - Mode2 == hc_andnegmode(Mode1,Mode2)

FUN * : filemode ** filemode -> filemode
DEF Mode1 * Mode2 == hc_andmode(Mode1,Mode2)

FUN in : permission ** filemode -> bool
DEF Perm in Mode == hc_inmode(Perm,Mode)

FUN stat : denotation -> com[filestat]
DEF File stat == call( hc_stat(File) )

FUN link : denotation ** denotation -> com[void]
DEF Old link New == call( hc_link(Old,New) )

FUN unlink : denotation -> com[void]
DEF File unlink == call( hc_unlink(File) )

FUN rename : denotation ** denotation -> com[void]
DEF Old rename New == call( hc_rename(Old,New) )

FUN touch : denotation -> com[void]
DEF File touch == call( hc_utime(File,nil,nil) )

FUN utime : denotation ** time ** time -> com[void]
DEF utime(File,ATime,MTime) == call( hc_utime(File,ATime avail,MTime avail) )

FUN chmod : denotation ** filemode -> com[void]
DEF File chmod Mode == call( hc_chmod(File,Mode) )

FUN chown : denotation ** userid -> com[void]
DEF File chown UserId == call( hc_chown(File,UserId avail,nil) )

FUN chgrp : denotation ** groupid -> com[void]
DEF File chgrp GroupId == call( hc_chown(File,nil,GroupId avail) )

FUN mkDir : denotation ** filemode -> com[void]
DEF Dir mkDir Mode == call( hc_mkdir(Dir,Mode) )

FUN rmDir : denotation -> com[void]
DEF Dir rmDir == call( hc_rmdir(Dir) )

FUN readDir : denotation -> com[seq[denotation]]
DEF Dir readDir ==
  call( hc_readdir(Dir) ) & (\\DArray.
  yield(okay( DArray asSeq ))
  )

FUN symLink : denotation ** denotation -> com[void]
DEF Old symLink New == call( hc_symlink(Old,New) )

FUN readLink : denotation -> com[denotation]
DEF Link readLink == call( hc_readlink(Link) )

FUN mkFifo : denotation ** filemode -> com[void]
DEF Fifo mkFifo Mode == call( hc_mkfifo(Fifo,Mode) )

FUN tmpName : com[denotation]
DEF tmpName == call( hc_tmpnam )

-- old fashioned

FUN delete : string -> com[void]
DEF File delete == unlink(File `)

FUN rename : string ** string -> com[void]
DEF Old rename New == rename(Old `,New `)

FUN tmpName : com[string]
DEF tmpName == tmpName & (\\Deno. yield(okay(Deno !)))


-- INTERNAL FUNCTIONS --

FUN ftord : filetype -> nat
DEF directory    ftord == 0
DEF regular      ftord == 1
DEF symLink      ftord == 2
DEF fifo         ftord == 3
DEF socket       ftord == 4
DEF charSpecial  ftord == 5
DEF blockSpecial ftord == 6
DEF unknown      ftord == 7

/$ handcoded [hc_emptymode, hc_nonemode, hc_addmode, hc_delmode, hc_ormode,
    hc_andnegmode, hc_andmode, hc_inmode, hc_stat, hc_link, hc_unlink, 
    hc_rename, hc_utime, hc_chmod, hc_chown, hc_mkdir, hc_rmdir,
    hc_readdir,  hc_symlink, hc_readlink, hc_mkfifo, hc_tmpnam] $/

FUN hc_emptymode : filemode
FUN hc_nonemode : filemode -> bool
FUN hc_addmode : filemode ** permission -> filemode
FUN hc_delmode : filemode ** permission -> filemode
FUN hc_ormode : filemode ** filemode -> filemode
FUN hc_andnegmode : filemode ** filemode -> filemode
FUN hc_andmode : filemode ** filemode -> filemode
FUN hc_inmode : permission ** filemode -> bool
FUN hc_stat : denotation -> void -> ans[filestat]
FUN hc_link : denotation ** denotation -> void -> ans[void]
FUN hc_unlink : denotation -> void -> ans[void]
FUN hc_rename : denotation ** denotation -> void -> ans[void]
FUN hc_utime : denotation ** option[time] ** option[time] -> void -> ans[void]
FUN hc_chmod : denotation ** filemode -> void -> ans[void]
FUN hc_chown : denotation ** option[userid] ** option[groupid] ->
               void -> ans[void]
FUN hc_mkdir : denotation ** filemode -> void -> ans[void]
FUN hc_rmdir : denotation -> void -> ans[void]
FUN hc_readdir : denotation -> void -> ans[array[denotation]]
FUN hc_symlink : denotation ** denotation -> void -> ans[void]
FUN hc_readlink : denotation -> void -> ans[denotation]
FUN hc_mkfifo : denotation ** filemode -> void -> ans[void]
FUN hc_tmpnam : void -> ans[denotation]

