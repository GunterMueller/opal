IMPLEMENTATION Time
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date: 1998-06-16 16:00:19 $ ($Revision: 1.1.1.1 $)
-- low-level access to system time
   /$ handcoded $/

IMPORT
  Void                ONLY void
  Nat                 ONLY nat 0 1 2 3 4 5 6 < =
  Com                 ONLY com ans call


-- EXPORTED TYPES --

-- DATA time == handcoded

DATA wday == sun mon tue wed thu fri sat


-- INTERNAL TYPES --

DATA tm == tm( tmsec:  nat,          -- 0 - 61
               tmmin:  nat,          -- 0 - 59
               tmhour: nat,          -- 0 - 23
               tmday:  nat,          -- 1 - 31
               tmmon:  nat,          -- 1 - 12
               tmyear: nat,          -- e.g. 1994
               tmwday: wday,
               tmyday: nat,          -- 1 - 366
               tmdst?: option[bool]  -- sometimes not available
             )


-- EXPORTED FUNCTIONS --

-- FUN time : com[time]
DEF time == call( hc_time )

-- FUN < <= = |= >= > : time ** time -> bool
-- DEF ... == handcoded

-- FUN - : time ** time -> real
-- DEF Time1 - Time2 == handcoded

-- FUN < : wday ** wday -> bool
DEF WDay1 < WDay2 == ord(WDay1) < ord(WDay2)

-- FUN = : wday ** wday -> bool
DEF WDay1 = WDay2 == ord(WDay1) = ord(WDay2)

-- FUN sec : time -> nat
DEF sec(Time) == hc_asTm(Time) tmsec

-- FUN min : time -> nat
DEF min(Time) == hc_asTm(Time) tmmin

-- FUN hour : time -> nat
DEF hour(Time) == hc_asTm(Time) tmhour

-- FUN day : time -> nat
DEF day(Time) == hc_asTm(Time) tmday

-- FUN mon : time -> nat
DEF mon(Time) == hc_asTm(Time) tmmon

-- FUN year : time -> nat
DEF year(Time) == hc_asTm(Time) tmyear

-- FUN wday : time -> wday
DEF wday(Time) == hc_asTm(Time) tmwday

-- FUN yday : time -> yday
DEF yday(Time) == hc_asTm(Time) tmyday

-- FUN dst? : time -> option[bool]
DEF dst?(Time) == hc_asTm(Time) tmdst?


-- INTERNAL FUNCTIONS --

FUN hc_time : void -> ans[time]
-- DEF hc_time(_) == handcoded

FUN ord : wday -> nat
DEF sun ord == 0
DEF mon ord == 1
DEF tue ord == 2
DEF wed ord == 3
DEF thu ord == 4
DEF fri ord == 5
DEF sat ord == 6

FUN hc_asTm : time -> tm
-- DEF hc_asTm(Time) == handcoded
