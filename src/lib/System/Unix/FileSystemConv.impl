IMPLEMENTATION FileSystemConv
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date: 2001-05-09 13:24:48 $ ($Revision: 1.3 $)

IMPORT
  Denotation          ONLY init
  Char                ONLY char
  PrintableChar       ONLY - r w x S s T t
  Nat                 ONLY 0 1 2 3 4 5 6 7 8 9 =
  StringConv          ONLY `
  StringFormat        COMPLETELY
  BTUnion             COMPLETELY
  SysTimeConv         ONLY format
  UserAndGroupConv    COMPLETELY
  FileSystem          COMPLETELY


-- EXPORTED FUNCTIONS --

FUN ` : inode -> denotation
DEF INode ` == hc_convinode(INode)

FUN ` : device -> denotation
DEF Device ` == hc_convdevice(Device)

FUN ` : filetype -> denotation
DEF regular      ` ==  "-"
DEF directory    ` ==  "d"
DEF charSpecial  ` ==  "c"
DEF blockSpecial ` ==  "b"
DEF fifo         ` ==  "p"
DEF socket       ` ==  "s"
DEF symLink      ` ==  "l"
DEF unknown      ` ==  "?"

FUN ` : permission -> denotation
DEF setUIdOnExec ` == "S_ISUID"
DEF setGIdOnExec ` == "S_ISGID"
DEF stickyBit    ` == "S_ISVTX"
DEF ownerRead    ` == "S_IRUSR"
DEF ownerWrite   ` == "S_IWUSR"
DEF ownerExec    ` == "S_IXUSR"
DEF groupRead    ` == "S_IRGRP"
DEF groupWrite   ` == "S_IWGRP"
DEF groupExec    ` == "S_IXGRP"
DEF worldRead    ` == "S_IROTH"
DEF worldWrite   ` == "S_IWOTH"
DEF worldExec    ` == "S_IXOTH"

FUN ` : filemode -> denotation
DEF `(FMode) == 9 init (\\Idx.
                          IF Idx = 0 THEN
                            IF ownerRead in FMode THEN r
                                                  ELSE - FI
                          IF Idx = 1 THEN
                            IF ownerWrite in FMode THEN w
                                                   ELSE - FI
                          IF Idx = 2 THEN
                            IF ownerExec in FMode THEN
                              IF setUIdOnExec in FMode THEN s
                                                       ELSE x FI
                            ELSE
                              IF setUIdOnExec in FMode THEN S
                                                       ELSE - FI
                            FI
                          IF Idx = 3 THEN
                            IF groupRead in FMode THEN r
                                                  ELSE - FI
                          IF Idx = 4 THEN
                            IF groupWrite in FMode THEN w
                                                   ELSE - FI
                          IF Idx = 5 THEN
                            IF groupExec in FMode THEN
                              IF setGIdOnExec in FMode THEN s
                                                       ELSE x FI
                            ELSE
                              IF setGIdOnExec in FMode THEN S
                                                       ELSE - FI
                            FI
                          IF Idx = 6 THEN
                            IF worldRead in FMode THEN r
                                                  ELSE - FI
                          IF Idx = 7 THEN
                            IF worldWrite in FMode THEN w
                                                   ELSE - FI
                          IF Idx = 8 THEN
                            IF worldExec in FMode THEN
                              IF stickyBit in FMode THEN t
                                                    ELSE x FI
                            ELSE
                              IF stickyBit in FMode THEN T
                                                    ELSE - FI
                            FI
                          FI
                       )

FUN ` : filestat -> denotation
DEF FStat ` ==
  `( "%d/%d %d%d %n %d %d %n %d/%d/%d" format
     ( `(device(FStat)) u `(inode(FStat)) u `(type(FStat)) u `(mode(FStat)) u
       (nlinks(FStat) u `(owner(FStat)) u `(group(FStat)) u size(FStat) u
       (("%m%d%y %k:%M" format atime(FStat)) u
       ("%m%d%y %k:%M" format ctime(FStat)) u
       u("%m%d%y %k:%M" format mtime(FStat))
       ))
   ) )

-- INTERNAL FUNCTIONS --

/$ handcoded [hc_convinode, hc_convdevice] $/

FUN hc_convinode : inode -> denotation
FUN hc_convdevice : device -> denotation

