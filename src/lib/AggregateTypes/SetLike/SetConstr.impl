IMPLEMENTATION SetConstr
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date: 1998-06-16 16:00:02 $ ($Revision: 1.1.1.1 $)

IMPORT Pair ONLY pair &
       Union2 ONLY union data1 data2
       SetMap ONLY *
       Set ONLY set + arb excl {} {}?

DEF S1 x S2 == IF S1 {}? THEN {}
	       ELSE LET e == arb(S1)
			S11 == excl(e, S1)
			S == S11 x S2
		    IN
		       (&(e,_) * S2) + S
               FI

DEF &(d11, d21) < &(d12, d22) == 
    IF d11 < d12 THEN true
    IF d12 < d11 THEN false
    ELSE d21 < d22
    FI

DEF S1 U S2 == data1(_) * S1 + data2(_) * S2

DEF data1(d11) < data1(d12) == d11 < d12
DEF data1(_)   < data2(_)   == true
DEF data2(_)   < data1(_)   == false
DEF data2(d21) < data2(d22) == d21 < d22
