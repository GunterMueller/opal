IMPLEMENTATION SeqFold
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date: 1999-09-23 15:16:06 $ ($Revision: 1.3 $)

IMPORT Seq[data] ONLY seq :: <>
       ABORT COMPLETELY

DEF + / <>                 == ABORT("/'SeqFold applied to empty sequence")
DEF + / (ft :: <>)         == ft
DEF + / (ft :: (sd :: rt)) == ft + ( + / (sd :: rt))

DEF + \ <>                 == ABORT("\\'SeqFold applied to empty sequence")
DEF + \ (ft :: <>)         == ft
DEF + \ (ft :: (sd :: rt)) == ifoldl(+, ft)(sd :: rt)

FUN ifoldl: (data ** data -> data) ** data -> seq[data] -> data
DEF ifoldl(+, accu)(<>) == accu
DEF ifoldl(+, accu)(ft :: rt) == ifoldl(+, accu + ft)(rt)
