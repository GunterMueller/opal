IMPLEMENTATION SeqFold
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date$ ($Revision$)

IMPORT Seq[data] ONLY seq :: <>
       ABORT COMPLETELY

DEF _ / <>                 == ABORT("/'SeqFold applied to empty sequence")
DEF _ / (ft :: <>)         == ft
DEF + / (ft :: (sd :: rt)) == ft + ( + / (sd :: rt))

DEF _ \ <>                 == ABORT("\\'SeqFold applied to empty sequence")
DEF _ \ (ft :: <>)         == ft
DEF + \ (ft :: (sd :: rt)) == ifoldl(+, ft)(sd :: rt)

FUN ifoldl: (data ** data -> data) ** data -> seq[data] -> data
DEF ifoldl(_, accu)(<>) == accu
DEF ifoldl(+, accu)(ft :: rt) == ifoldl(+, accu + ft)(rt)
