IMPLEMENTATION Bool
/* %- Copyright 1989 - 1998 by the Opal Group, TU Berlin. All rights reserved
      See OCSHOME/etc/LICENSE or 
      http://uebb.cs.tu-berlin.de/~opal/LICENSE.html for details
*/
-- % $Date: 1998-06-16 15:59:57 $ ($Revision: 1.1.1.1 $)

IMPORT Denotation ONLY =
       ABORT ONLY ABORT

DEF T == true:bool
DEF F == false:bool

DEF id(a) == a
DEF not == ~
DEF _ true == true
DEF _ false == false
DEF _ T == true
DEF _ F == false

DEF equiv == =
DEF xor == |=
DEF a nand b == ~(a) or ~(b)
DEF a nor b == ~(a) and ~(b)
DEF implies == <=
DEF => == <=

DEF /\ == and
DEF \/ == or
DEF /~\ == nand
DEF \~/ == nor

DEF a 1st _ == a
DEF _ 2nd b == b
DEF a n1st _ == ~(a)
DEF _ n2nd b == ~(b)

DEF _ true _ == true
DEF _ false _ == false
DEF _ T _ == true
DEF _ F _ == false


DEF false < true == true
DEF _ < _ == false

DEF _ <= true == true
DEF false <= false == true
DEF true <= false == false

DEF a > b == b < a
DEF a >= b == b <= a

DEF !(d) == IF d = "true" THEN true
            IF d = "false" THEN false
            ELSE ABORT("!'Bool: wrong denotation")
            FI