IMPLEMENTATION SortHandling

IMPORT Set ONLY exist? in excl set {}? arb {} - + {<}
       RepoName COMPLETELY
       RepoObjectAccess ONLY object impl kind
       RepoAbsy COMPLETELY
       Seq ONLY <> # forall? ++ seq :: = % find? exist? ::?
       SetFilter ONLY |
       SetMap ONLY *
       SetConv ONLY asSet
       SetOfSet ONLY flat
       Nat ONLY |= nat
       SeqOfSeq ONLY partition flat
       FormulaHandling ONLY propertyS
       SeqMap ONLY *
       Option ONLY option nil? cont nil avail
       ABORT ONLY ABORT
       NameHandling ONLY instantiate
       DEBUG ONLY PRINT
       String ONLY string
       Fmt ONLY pretty fmt
       FmtSeq ONLY fmtSeq
       Denotation ONLY ++


-- %$Indirect Recursion$
DEF indirectRec?(oN)(R) ==
  exist?((\\ s. oN in reachable(s)(R)), excl(oN, directReachable(oN)(R)))

-- %return direct reachable sorts from the same structure
FUN directReachable: objectName -> repo -> set[objectName, <]
DEF directReachable(oN)(R) ==
  LET srt == object(oN)(R)
      vars == IF impl(srt) data? THEN variants(impl(srt))
              OTHERWISE
              IF properties(srt) {}? THEN <>
              ELSE variants(propertyS(arb(properties(srt)))(R))
              FI
  IN
  (\\newSort. unitName(newSort) ~ unitName(oN)) |
  asSet((flat(names * fct * kind * 
                     (\\ oN1. object(oN1)(R)) * (constructor * vars))))

FUN names: fct -> seq[objectName]
DEF names(name(n)) == %(n)
DEF names(cart(els)) == flat(names * els)
DEF names(funct(d,c)) == names(d) ++ names(c)

FUN reachable: objectName -> repo -> set[objectName, <]
DEF reachable(oN)(R) == reachable({}, R)(oN)

FUN reachable: set[objectName, <] ** repo -> objectName -> set[objectName, <]
DEF reachable(already, R)(self) ==
  LET cands == directReachable(self)(R)
      new == cands - already
  IN
  already + flat(reachable(already + cands, R) * new)


-- %$Equivalence$

DEF ~~(R)(vs1, vs2) ==
  IF #(vs1) |= #(vs2) THEN false
  ELSE
    forall?(~(R), partition((\\x, y. constructor(x) = constructor(y)))
                           (vs1 ++ vs2))
  FI

-- %constructors ok? 
FUN ~ : repo -> seq[variant] -> bool
DEF ~(_R)(v1 :: v2 :: <>) == 
  IF test(v1) = test(v2) THEN =(~)(components(v1),components(v2))
  ELSE false
  FI
DEF ~(_)(_) == false

FUN ~ : component ** component -> bool
DEF component(_, sel1) ~ component(_, sel2) == sel1 = sel2


DEF variants(oN, R) ==
  LET props == properties(object(oN)(R))
  IN
  IF props {}? THEN <> -- no free type information
  IF ~(kind(object(oN)(R)) sort?) THEN <> -- ? no sort
  ELSE
    LET vs == variants(propertyS(arb(props))(R))
    IN
    IF localObjectName?(oN) THEN vs
    IF globalObjectName?(oN) THEN
      IF derivedUnitName?(unit(oN)) THEN instantiate(instNo(unit(oN))) * vs
      ELSE vs
      FI
    FI
  FI

DEF dataVariants(oN, R) ==
  LET i == impl(object(oN)(R))
  IN
  IF i data? THEN 
    LET vs == variants(i)
    IN
    IF localObjectName?(oN) THEN vs
    IF globalObjectName?(oN) THEN
      IF derivedUnitName?(unit(oN)) THEN instantiate(instNo(unit(oN))) * vs
      ELSE vs
      FI
    FI
  ELSE <> -- not a sort or not implemented
  FI


DEF find(R)(oN) ==
  LET k == kind(object(oN)(R))
  IN
  IF k sort? THEN nil 
  ELSE
    LET f == fct(k)
    IN
    IF f name? THEN avail(sort(f))
    IF f funct? ANDIF name?(codom(f)) THEN avail(sort(codom(f)))
    ELSE nil
    FI
  FI

DEF findD(R)(oN) ==
  LET k == kind(object(oN)(R))
  IN
  IF k sort? THEN nil 
  ELSE
    LET f == fct(k)
    IN
    IF f name? THEN avail(sort(f))
    IF f funct? ANDIF name?(dom(f)) THEN avail(sort(dom(f)))
    ELSE nil
    FI
  FI


DEF findCon(R)(con, variants) ==
  find?(\\v. baseName(constructor(v)) = baseName(con), variants)

DEF findDis(R)(dis, variants) ==
   find?(\\v. baseName(test(v)) = baseName(dis), variants)

DEF findSel(R)(sel, <>) == nil
DEF findSel(R)(sel, var :: variants) ==
  LET erg == find?(\\v. baseName(selector(v)) = baseName(sel), components(var))
  IN
  IF erg nil? THEN findSel(R)(sel, variants)
  ELSE erg
  FI

DEF constr?(oN)(R) ==
  LET boN == baseName(oN)
      vs == sVariants(boN, R)
      vs0 == PRINT(traceSH, \\ . "constr?: " ++
                  pretty(fmtSeq(fmtObjectName)(constructor * vs)) ++
                  " " ++ 
                  pretty(fmtObjectName(oN)) ++ "/" ++
                  pretty(fmtObjectName(boN)),
                  vs)
  IN
  exist?(\\ oNN. oNN = boN, constructor * vs0)

DEF sel?(oN)(R) ==
  LET boN == baseName(oN)
      vs == tVariants(boN, R)
  IN
  exist?(\\ oNN. oNN = boN, selector * flat(components * vs))

DEF discr?(oN)(R) ==
  LET boN == baseName(oN)
      vs == tVariants(boN, R)
  IN
  exist?(\\ oNN. oNN = boN, test * vs)

-- %variants of type of single codomain functionality
FUN sVariants: objectName ** repo -> seq[variant]
DEF sVariants(boN, R) ==
  LET k == kind(boN)(R)
  IN
  IF k oper? ANDIF fct(k) funct? ANDIF name?(codom(fct(k))) THEN
    PRINT(traceSH, \\ . "sVariants:ok2", xVariants(sort(codom(fct(k))), R))
  IF k oper? ANDIF name?(fct(k)) THEN
    PRINT(traceSH, \\ . "sVariants:okC", xVariants(sort(fct(k)), R))
  ELSE
    PRINT(traceSH,  \\ . "sVariants:fail:" ++ pretty(fmtKind(k)), <>)
  FI

-- %variants of type of single domain functionality
FUN tVariants: objectName ** repo -> seq[variant]
DEF tVariants(boN, R) ==
  LET k == kind(boN)(R)
  IN
  IF k oper? ANDIF fct(k) funct? ANDIF name?(dom(fct(k))) THEN
    xVariants(sort(dom(fct(k))), R)
  ELSE
    <>
  FI
  

FUN xVariants: objectName ** repo -> seq[variant]
DEF xVariants(oN, R) ==
  IF dataVariants(oN, R) ::? THEN dataVariants(oN, R)
  ELSE variants(oN, R)
  FI

-- %$Check for free type$
DEF freeTV?(R)(oN) ==
  LET s == sort(fct(kind(oN)(R)))
  IN freeT(s)(R)

FUN freeT: objectName -> repo -> bool
DEF freeT(s)(R) ==
  LET props == properties(object(s)(R))
  IN
  exist?(\\ pN. freeType?(propertyS(pN)(R)), props)

FUN traceSH: bool
DEF traceSH == false