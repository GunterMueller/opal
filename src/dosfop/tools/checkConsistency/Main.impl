IMPLEMENTATION Main
-- $Header: /home/florenz/opal/home_uebb_CVS/CVS/ocs/src/dosfop/tools/checkConsistency/Main.impl,v 1.2 1999-03-09 11:46:24 kd Exp $

IMPORT Nat              COMPLETELY
       Char             COMPLETELY
       ComCompose       COMPLETELY
       Com              COMPLETELY
       String           COMPLETELY
       StringConv       COMPLETELY
       Seq              COMPLETELY
       SeqMap           COMPLETELY
       ProcessArgs      COMPLETELY
       Set              COMPLETELY
       Stream           COMPLETELY
       Denotation       COMPLETELY

IMPORT Check            COMPLETELY
       Config           COMPLETELY
       ConfigFun        COMPLETELY
       MyComStream      COMPLETELY 
       Error            COMPLETELY
       ScanConfig       COMPLETELY
       ParseConfig      COMPLETELY


DEF checkConsistency ==
   arg(1) ; 
   (\\ pathResult . 
      IF okay?(pathResult) THEN
        (writeLine(stdErr, "start processing...") &
        readFile(!(data(pathResult) ++ "DOSFOP/config"))) ;
        (\\readConfigResult.
           IF okay?(readConfigResult) THEN
             LET
               (tokenSeq, res) == scanConfig(data(readConfigResult))
             IN
               IF ok?(res) THEN
                 writeLine(stdErr, "scanning of configuration ok ...") & (\\ _ .
                 LET
                   (config, res2) == parseConfig(tokenSeq)
                 IN
                   IF ok?(res2) THEN
                     writeLine(stdErr, "parsing of configuration ok ...") & 
                       checkPresence(topStructureName(config), config)
                   ELSE
                     write(stdErr, reason(res2))
                   FI
                 ) -- \\ _
               ELSE
                 write(stdErr, reason(res))
               FI
           ELSE
             writeLine(stdErr, error(readConfigResult))
           FI
         ) -- \\readConfigResult
    ELSE
      writeLine(stdErr, error(pathResult))
    FI
  ) -- \\pathResult
