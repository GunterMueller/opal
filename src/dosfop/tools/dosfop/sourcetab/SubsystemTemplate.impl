IMPLEMENTATION SubsystemTemplate[ structure, emptySource ]
-- %- $Id$

IMPORT 
  String 	COMPLETELY       SeqMap	COMPLETELY
  Seq		COMPLETELY       Option	COMPLETELY
       
IMPORT
  SubsystemType	COMPLETELY       Config	COMPLETELY

/* %An empty subsystem configuration leads to an empty subsystem source-table 
representation: */
DEF makeSubsystemTemplate( <>'Seq[subsystem] ) == <>

/* %We establish an empty structure placeholder for every specified subsystem-structure
and apply @c{makeSubsystemTemplate} on every sub-subsystem and on all remaining
subsystems of the same level recursively. */
DEF makeSubsystemTemplate
 ( subsystem(actName, _, _, _, actStructs, subSubsystems) :: rtSubsystems ) == 
    subsystem(
      actName,
      <>,
      ( \\structName. emptySource(structName)) * structureNames,
      makeSubsystemTemplate(subSubsystems)
     ) ::
       makeSubsystemTemplate(rtSubsystems)
  WHERE 
    structureNames == IF nil?(actStructs) THEN
                        <>
                      ELSE
                        name * structs(cont(actStructs))
                      FI












