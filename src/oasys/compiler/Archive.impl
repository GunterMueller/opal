IMPLEMENTATION Archive

IMPORT  Pair                    COMPLETELY
        Set                     COMPLETELY
        SetReduce               COMPLETELY
        SetFilter               COMPLETELY
        Pair                    COMPLETELY
        Option                  COMPLETELY
        Seq                     COMPLETELY
        SeqReduce               COMPLETELY
        SeqMap                  ONLY *
        SetConv                 ONLY asSeq
        SetReduce               COMPLETELY
        SetFilter               COMPLETELY
        NatConv                 COMPLETELY
        Denotation              COMPLETELY

IMPORT  Com                     COMPLETELY
        ComCheck                ONLY |
        ProcessArgs             COMPLETELY
        ComCompose              COMPLETELY
        ComPairCompose          COMPLETELY
        FileName                COMPLETELY
        File                    ONLY stdIn stdOut stdErr

IMPORT  RepoUnitAccess          COMPLETELY
        RepoDiag                COMPLETELY
        RepoComAbort            COMPLETELY
        RepoEvent               COMPLETELY
        RepoEvent               COMPLETELY
        RepoName                COMPLETELY
        RepoGlobalAccess        COMPLETELY
        RepoAdmin               COMPLETELY

IMPORT  InternExec              COMPLETELY
        Check                   COMPLETELY
        Compile                 COMPLETELY

FUN structsOfFolder : folder -> repo -> set[unitName,<]
DEF structsOfFolder(F)(R) ==
    (\\U. ~(library?(U)(R)) and ~(sign?(part(U)))
                            and dirName(path(sourceFile(U)(R))) = sourcePath(F))
        | units(R)

DEF guessArchiveFolder(F)(R) ==
    guessCompileUnits(guessCheckUnits(structsOfFolder(F)(R))(R))(R)

DEF archiveFolder(F)(R) ==
    clearErrors                                 & (\\ _ .
    LET (Check, Compile) == unpair(guessArchiveFolder(F)(R))
    IN
    compileUnits(Check + Compile)(R)            & (\\ R1 .
    errors?                                     & (\\ Err? .
    IF Err? THEN
        succeed(R1)
    ELSE
        oasysLibDir                             & (\\ Dir .
        LET SysName == baseName(dirName1(dirName1(objectPath(F))))
        IN
        event(progress(repository, "archiving " ++ SysName))(R1)
                                                & (\\ R2.
        call(normPath(Dir) ++ "archiver", 
             "archiver" :: 
             SysName :: objectPath(F) :: 
             (\\U. baseName(path(objectFile(U)(R2)))) 
                        * asSeq(structsOfFolder(F)(R2)),
             stdIn, stdOut, stdErr)(R2)         ; (\\ Ans? .
        IF okay?(Ans?) THEN
            succeed(R2)
        ELSE
            diag(!!(error,freeRef("archive"), error(Ans?)))(R2)
        FI
        )))
    FI
    )))

