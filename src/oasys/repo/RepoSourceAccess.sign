/* % This structure provides access functions related to the source. */

SIGNATURE RepoSourceAccess

IMPORT  Set[objectName,<]       ONLY set
        Set[itemName,<]         ONLY set
        Set[denotation,<]       ONLY set
        Denotation              ONLY <
        Seq[itemName]           ONLY seq
        Seq[item]               ONLY seq
        Option[itemName]        ONLY option
   
IMPORT  Repo			ONLY repo:SORT
        RepoName                ONLY unitName:SORT 
                                        < : unitName ** unitName -> bool
                                     objectName:SORT
                                        < : objectName ** objectName -> bool
                                     itemName:SORT
                                        < : itemName ** itemName -> bool
        RepoSource              ONLY item:SORT 


/* % @code{items} delivers the set of items associated with a unit or
object. @code{itemOrder} returns the ordering of
items. @code{itemImports} returns the set of symbolic names used in
the import items of the unit. */

FUN items       : unitName              -> repo -> set[itemName,<]
FUN items       : objectName            -> repo -> set[itemName,<]
FUN itemOrder   : unitName              -> repo -> seq[itemName]
FUN itemImports : unitName              -> repo -> set[denotation,<]


/* % @code{item} returns the item associated with a given item name: */

FUN item        : itemName              -> repo -> item


/* % @code{newItems} adds a number of items to the given unit.
@code{updateItem} updates an item, @code{updateItemOrder} changes the
item order, and @code{removeItem} removes an item. */

FUN newItems     : unitName ** seq[item]        -> repo -> seq[itemName]
                                                                   ** repo
FUN updateItem  : itemName ** (item -> item)    -> repo -> repo
FUN updateItemOrder : unitName ** (seq[itemName] -> seq[itemName])
                                                -> repo -> repo
FUN removeItem  : itemName                      -> repo -> repo

/* % @code{assocItem} associates an item with a set of objects: */

FUN assocItem   : itemName ** set[objectName,<] -> repo -> repo



