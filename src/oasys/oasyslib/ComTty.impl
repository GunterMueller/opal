IMPLEMENTATION ComTty

IMPORT  Com             COMPLETELY
        ComCompose      COMPLETELY
        File            COMPLETELY
        Char            COMPLETELY
        Option          COMPLETELY
        ComState        COMPLETELY

FUN versus: com[data] ** com[data] -> com[data]
DEF versus(Yes,No) ==
    yes? & (\\Yes?. IF Yes? THEN Yes ELSE No FI )

FUN yes? : com[bool]
DEF yes? == read(stdIn) & (\\Char. succeed(Char yes?))

        FUN yes? : char -> bool
        DEF yes?(C) == C = !("y") or C = !("Y") 

FUN onInteractive : com[option[data]] -> com[option[data]]
DEF onInteractive(Cmd) ==
    interactive?                        & (\\Interact?.(
    IF Interact?
      THEN Cmd 
      ELSE succeed(nil)
    FI)                                 )


FUN &? : com[option[data]] ** com[data] -> com[data]
DEF Cmd1 &? Cmd2 ==
    Cmd1                                & (\\OptRes.
    IF OptRes avail?
      THEN succeed(cont(OptRes))
      ELSE Cmd2
    FI                                  )

-- -------------------------------------------------
FUN interactive : bool                       -> com[void]
DEF interactive(Flag) == interactiveID => (\\ _ . Flag)
 
FUN interactive?   :                               com[bool]
DEF interactive? == get(interactiveID)
 
        FUN interactiveID : stateId[bool]
        DEF interactiveID == EXEC(stateId(true))
 
