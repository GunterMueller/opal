IMPLEMENTATION TIVI

IMPORT AEntry COMPLETELY
       Com ONLY com exit break succeed EXEC
       ComAgent ONLY agent
       ComCheckWin ONLY check notify
       ComCompose ONLY &
       Env ONLY arg argCount
       Id ONLY id
       InterOpalEntry COMPLETELY
       InterOpalIO ONLY readInterOpal
       Nat ONLY nat 0 100 20 - 1 > pred 64 128
       OWmisc ONLY label 
       Option COMPLETELY
       ProcessCtrl ONLY getCWD
       Void COMPLETELY
       WinButton COMPLETELY
       WinConfig COMPLETELY
       WinEmitter COMPLETELY
       WinEvent COMPLETELY
       WinRegulator COMPLETELY
       WinView COMPLETELY
       WinWindow COMPLETELY
       Denotation ONLY ++ empty
       MONITOR ONLY genMonitor
       WinImage ONLY image photo file iconfig
       Seq ONLY .. seq
       SeqFold ONLY /
       ComTimeout ONLY timeout
       ComAction ONLY done

DEF tivi2 ==
    genMonitor(false) & (\\ _ .
    aEntryLong[denotation, mypreparse, myprint](64) & (\\fname.
    emitter[void] & (\\quit.
    emitter[void] & (\\trg.
    photo(file("/home/uebb/kd/ocs/local/src/TIVI/tivi.gif")) & (\\ p.
    regulator(fname) set bind(key("Return"), \\ _ . trg emit nil) ++ 
                         bind(key("KP_Enter"), \\ _ . trg emit nil) & (\\ _ .
    argCount & (\\ ac.
    (
    IF ac > 1 THEN 
        (& / (1 .. pred(ac))(\\ i. arg(i) & (\\fn. showIO(fn))))
        & (arg(1) & (\\fn. agent(fname set fn) & succeed(state(iconic))))
    ELSE
      agent(getCWD & (\\ cwd. fname set cwd)) & succeed(state(normal))
    FI) & ( \\ initState .
    LET title == "TIVI -- The InterOpal Viewer and Editor (InterOpal2)"
        v == (label with text(title)) ^-^
             (label with image(p))
        load == button("Load and View", trg emit nil)
        quitb == button("Quit", quit emit nil)
    IN
    agent(ctrl(trg, fname)) & (\\ _ .
    window(initState ++ 
           titleName(title) ++ 
           iconName("TIVI/E") ++ 
           icon(file("/home/uebb/kd/ocs/local/src/TIVI/tivi.xbm")), 
           v ^-^ label("Filename:", view(fname)) ^-^ bild ^-^ 
        (  load <|< quitb)) & (\\w.
    focus(w) & (\\ _ .
    await(quit) & (\\ _ .
    delete(w) & (\\ _ .
    exit(0)
    )))))
    ))))))
    ))

FUN 80:nat
DEF 80 == 100 - 20

FUN ctrl: emitter[void] ** aEntry[denotation, mypreparse, myprint] -> com[void]
DEF ctrl(trg, fname) ==
    await(trg) & (\\ _ .
    agent(ctrl(trg, fname)) & (\\ _ .
    get(fname) & (\\fn.
    showIO(fn)
    )))

FUN showIO: denotation -> com[void]
DEF showIO(fn) ==
    notify(
    readInterOpal(false, fn) & (\\inop.
    (IF inop nil? THEN break("Could not read InterOpal file")
     ELSE interOpalWindow(fn, cont(inop))
     FI
    )))
    

FUN mypreparse: denotation -> option[denotation] ** denotation
DEF mypreparse(d) == (avail(d), empty)

FUN myprint: denotation -> denotation
DEF myprint == id

FUN bild: view
DEF bild == EXEC(
            photo(file("/home/uebb/kd/ocs/src/TIVI/tivi.gif")) & (\\ p.
            succeed(frame with image(p)))
            )